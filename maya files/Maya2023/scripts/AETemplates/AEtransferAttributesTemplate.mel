// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//	Creation Date:	Febuary 27, 2006
//
//	Procedure Name:
//	AEtransferAttributesTemplate
//
//	Description Name;
//	Creates the attribute editor controls for the transfer attributes node
//
//	Input Value:
//	nodeName
//
//	Output Value:
//	None
//

global proc checkTransferAttributesSampleSpace( string $nodeName )
{
	string $nodeAttr = $nodeName + ".sampleSpace";
	int $value = `getAttr $nodeAttr`;
	if ( $value == 3) 
	{
		editorTemplate -dimControl $nodeName "searchMethod" true;
		editorTemplate -dimControl $nodeName "sourceUVSpace" false;
		editorTemplate -dimControl $nodeName "targetUVSpace" false;
		editorTemplate -dimControl $nodeName "searchScale" false;
	} 
	else if ( $value == 4) 
	{
		editorTemplate -dimControl $nodeName "searchMethod" true;
		editorTemplate -dimControl $nodeName "sourceUVSpace" true;
		editorTemplate -dimControl $nodeName "targetUVSpace" true;
		editorTemplate -dimControl $nodeName "searchScale" true;
	} 
	else 
	{
		editorTemplate -dimControl $nodeName "searchMethod" false;
		editorTemplate -dimControl $nodeName "sourceUVSpace" true;
		editorTemplate -dimControl $nodeName "targetUVSpace" true;
		editorTemplate -dimControl $nodeName "searchScale" false;
	}
	if ( $value == 5) 
	{
		editorTemplate -dimControl $nodeName "matchChoice" false;
	}
	else
	{
		editorTemplate -dimControl $nodeName "matchChoice" true;
	}
}

global proc checkTransferAttributesUVSampling( string $nodeName )
{
	string $nodeAttr = $nodeName + ".transferUVs";
	int $value = `getAttr $nodeAttr`;
	if ( $value == 1) 
	{
		editorTemplate -dimControl $nodeName "sourceUVSet" false;
		editorTemplate -dimControl $nodeName "targetUVSet" false;
	} 
	else 
	{
		editorTemplate -dimControl $nodeName "sourceUVSet" true;
		editorTemplate -dimControl $nodeName "targetUVSet" true;
	}
	if ( $value == 0)
	{
		editorTemplate -dimControl $nodeName "flipUVs" true;
	}
	else
	{
		editorTemplate -dimControl $nodeName "flipUVs" false;
	}
}

global proc checkTransferAttributesColorSampling( string $nodeName )
{
	string $nodeAttr = $nodeName + ".transferColors";
	int $value = `getAttr $nodeAttr`;
	if ( $value == 1) 
	{
		editorTemplate -dimControl $nodeName "sourceColorSet" false;
		editorTemplate -dimControl $nodeName "targetColorSet" false;
	} 
	else 
	{
		editorTemplate -dimControl $nodeName "sourceColorSet" true;
		editorTemplate -dimControl $nodeName "targetColorSet" true;
	}
	if ( $value == 0)
	{
		editorTemplate -dimControl $nodeName "colorBorders" true;
	}
	else
	{
		editorTemplate -dimControl $nodeName "colorBorders" false;
	}
}

global proc AEtransferAttributesTemplate( string $nodeName )
{
	editorTemplate -beginScrollLayout;


		editorTemplate -beginLayout (uiRes("m_AEtransferAttributesTemplate.kAttributesToTransfer")) -collapse off;

			editorTemplate -addControl "transferPositions";
			
			editorTemplate -addControl "transferNormals";
			
			editorTemplate -addControl "transferUVs" "checkTransferAttributesUVSampling";
			editorTemplate -addControl "sourceUVSet";
			editorTemplate -addControl "targetUVSet";

			editorTemplate -addControl "transferColors" "checkTransferAttributesColorSampling";
			editorTemplate -addControl "sourceColorSet";
			editorTemplate -addControl "targetColorSet";

		editorTemplate -endLayout;

		editorTemplate -beginLayout (uiRes("m_AEtransferAttributesTemplate.kSamplingOptions")) -collapse off;
		
			editorTemplate -addControl "sampleSpace" "checkTransferAttributesSampleSpace";
			editorTemplate -addControl "matchChoice";
			editorTemplate -addControl "sourceUVSpace";
			editorTemplate -addControl "targetUVSpace";
			editorTemplate -addControl "searchScale";
			editorTemplate -addControl "flipUVs";
			editorTemplate -addControl "colorBorders";
			editorTemplate -addControl "searchMethod";
		
		editorTemplate -endLayout;

		// supressed attributes
		editorTemplate -suppress "input";
		editorTemplate -suppress "inputMatrix";
		editorTemplate -suppress "inputComponents";
		editorTemplate -suppress "source";
		editorTemplate -suppress "sourceMatrix";

		// throw in everything else we don't want to explicitly setup
		editorTemplate -addExtraControls;

	editorTemplate -endScrollLayout;

}
