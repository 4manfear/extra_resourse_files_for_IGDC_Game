// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//	Procedure Name:
//	AEcloseCurveTemplate
//
//	Description Name;
//	Creates the attribute editor controls for the closeCurve node
//
//	Input Value:
//	nodeName
//
//	Output Value:
//	None
//

global proc closeCurveBlendKnotInsertionControls( string $nodeName )
{
    int $m = `getAttr ($nodeName + ".blendKnotInsertion")`;
    if( 0 == $m ) {
        editorTemplate -dimControl $nodeName "parameter" true;
    }
    else {
        editorTemplate -dimControl $nodeName "parameter" false;
    }
}

global proc closeCurveMethodControls( string $nodeName )
{
    int $m = `getAttr ($nodeName + ".preserveShape")`;
    if( 2 == $m ) {
        editorTemplate -dimControl $nodeName "blendBias" false;
        editorTemplate -dimControl $nodeName "blendKnotInsertion" false;
        closeCurveBlendKnotInsertionControls( $nodeName );
	}
	else {
        editorTemplate -dimControl $nodeName "blendBias" true;
        editorTemplate -dimControl $nodeName "blendKnotInsertion" true;
        editorTemplate -dimControl $nodeName "parameter" true;
    }
}

global proc AEcloseCurveTemplate( string $nodeName )
{
	string $inputCurve = (uiRes("m_AEcloseCurveTemplate.kInputCurve"));
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout (uiRes("m_AEcloseCurveTemplate.kCloseCurveistory")) -collapse 0;
		
			editorTemplate -callCustom ("AEinputNew \""+$inputCurve+"\"")
					("AEinputReplace \""+$inputCurve+"\"")
					"inputCurve";
			editorTemplate -addControl "preserveShape"
				"closeCurveMethodControls";

            editorTemplate -addControl "blendBias";
            editorTemplate -addControl "blendKnotInsertion"
                "closeCurveBlendKnotInsertionControls";
            editorTemplate -addControl "parameter";

		editorTemplate -endLayout;

		// include/call base class/node attributes
		AEdependNodeTemplate $nodeName;

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}
