// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:	March 18, 2009
//
//  Procedure Name:
//	AEviewColorManagerTemplate
//
//  Description:
//	Creates the attribute editor controls for the viewColorManager node
//
//  Input Value:
//	nodeName
//
//  Output Value:
//	None
//

global proc AElutBrowser( string $lutAttribute )
{
		string $all = (uiRes("m_AEviewColorManagerTemplate.kAll"));
	    string $lutFilters = 
        ("lustre (*.3dl);;" + "csp (*.csp);;" + "iridas (*.look);;" + "arricube (*.cube);;" + "discreet (*.lut);;" + "avid (*.lut);;" + $all + "(*.*)");

    string $result[] = `fileDialog2 
        -caption (uiRes("m_AEviewColorManagerTemplate.kOpenLut"))
        -fileMode 1
        -fileFilter $lutFilters
        -selectFileFilter "lustre"`;

    string $filename = "";
    if (size($result) > 0)
    {
        $filename = $result[0];
    }

    setAttr $lutAttribute -type "string" $filename;

}

global proc AElutFileNew( string $lutAttribute )
{
    setUITemplate -pst attributeEditorTemplate;
    rowLayout -nc 3 textureNameLayout;
        text -label (uiRes("m_AEviewColorManagerTemplate.kLutFile")) ;
        textField lutFileField;
        symbolButton -image "navButtonBrowse.png" browser;
        setParent ..;
    setUITemplate -ppt;

    AElutFileReplace $lutAttribute;
}

global proc AElutFileReplace (string $lutAttribute)
{
    connectControl -fileName lutFileField $lutAttribute;

    button -e -c
        ("AElutBrowser \"" + $lutAttribute + "\"" ) browser;
}


global proc AEviewColorManagerTemplate ( string $nodeName )
{
    editorTemplate -beginScrollLayout;
	
    editorTemplate -addControl "imageColorProfile";
    editorTemplate -addControl "displayColorProfile";
    editorTemplate -suppress "exposure";
    editorTemplate -suppress "contrast";
    editorTemplate -callCustom "AElutFileNew" "AElutFileReplace" 
        "lutFile";

	//suppressed attributes
	editorTemplate -suppress "contrastPivot";

    editorTemplate -addExtraControls;
    editorTemplate -endScrollLayout;
}
