// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//  Procedure Name:
//	AEattachCurveTemplate
//
//  Description Name;
//	Creates the attribute editor controls for the attachCurve Node
//
//  Input Value:
//	nodeName 
//
//  Output Value:
//	None
// 


global proc attachCurveBlendKnotInsertionControls( string $nodeName )
{
	int $m = `getAttr ($nodeName + ".blendKnotInsertion")`;
	if( 0 == $m ) {
		editorTemplate -dimControl $nodeName "parameter" true;
	}
	else {
		editorTemplate -dimControl $nodeName "parameter" false;
	}
}

global proc attachCurveMethodControls( string $nodeName )
{
	int $m = `getAttr ($nodeName + ".method")`;
	if( 0 == $m ) {
		editorTemplate -dimControl $nodeName "keepMultipleKnots" false;
		editorTemplate -dimControl $nodeName "blendBias" true;
		editorTemplate -dimControl $nodeName "blendKnotInsertion" true;
		editorTemplate -dimControl $nodeName "parameter" true;
	}
	else {
		editorTemplate -dimControl $nodeName "keepMultipleKnots" true;
		editorTemplate -dimControl $nodeName "blendBias" false;
		editorTemplate -dimControl $nodeName "blendKnotInsertion" false;
		attachCurveBlendKnotInsertionControls( $nodeName );
	}
}

global proc AEattachCurveTemplate ( string $nodeName )
{
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout (uiRes("m_AEattachCurveTemplate.kAttachCurveHistory"))  -collapse 0;
		string $input1 = (uiRes("m_AEattachCurveTemplate.kInputCurve1"));
		string $input2 = (uiRes("m_AEattachCurveTemplate.kInputCurve2"));

			editorTemplate -callCustom ("AEinputNew \"" + $input1 + "\"")
					("AEinputReplace \"" + $input1 + "\"")
					"inputCurve1";
			editorTemplate -callCustom ("AEinputNew \"" + $input2 + "\"")
					("AEinputReplace \"" + $input2 + "\"")
					"inputCurve2";
			editorTemplate -addControl "reverse1";
			editorTemplate -addControl "reverse2";

			editorTemplate -addControl "method" "attachCurveMethodControls";

			editorTemplate -addControl "keepMultipleKnots";
			editorTemplate -addControl "blendBias";
			editorTemplate -addControl "blendKnotInsertion"
				"attachCurveBlendKnotInsertionControls";
			editorTemplate -addControl "parameter";

		editorTemplate -endLayout;

		// include/call base class/node attributes
		AEabstractBaseCreateTemplate $nodeName;

	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}
