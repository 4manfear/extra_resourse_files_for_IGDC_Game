// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
proc setOptionVars(int $forceFactorySettings)
{
	optionVar -init $forceFactorySettings -category "Modify.Match Translation"
		-iv matchTranslateX 1
		-iv matchTranslateY 1
		-iv matchTranslateZ 1
		;
}

global proc performMatchTranslateSetup(string $parent, int $forceFactorySettings)
{
	setOptionVars ($forceFactorySettings);
	setParent $parent;

	int $axisX = `optionVar -q matchTranslateX`;
	int $axisY = `optionVar -q matchTranslateY`;
	int $axisZ = `optionVar -q matchTranslateZ`;

	if ($axisX && $axisY && $axisZ) {
		checkBoxGrp -e -v1 true matchAllCheckBoxGrp;
		checkBoxGrp -e -v1 false -v2 false -v3 false matchXYZCheckBoxGrp;
	} else {
		checkBoxGrp -e -v1 false matchAllCheckBoxGrp;
		checkBoxGrp -e -v1 $axisX -v2 $axisY -v3 $axisZ matchXYZCheckBoxGrp;
	}
}

global proc performMatchTranslateCallback(string $parent, int $doIt)
{
	setParent $parent;

	if (`checkBoxGrp -q -v1 matchAllCheckBoxGrp`) {
		optionVar -iv matchTranslateX true;
		optionVar -iv matchTranslateY true;
		optionVar -iv matchTranslateZ true;
	} else {
		optionVar -iv matchTranslateX `checkBoxGrp -q -v1 matchXYZCheckBoxGrp`;
		optionVar -iv matchTranslateY `checkBoxGrp -q -v2 matchXYZCheckBoxGrp`;
		optionVar -iv matchTranslateZ `checkBoxGrp -q -v3 matchXYZCheckBoxGrp`;
	}

	if ($doIt) {
		performMatchTranslate 0; 
		addToRecentCommandQueue "performMatchTranslate 0" "MatchTranslation";
	}
}

proc performMatchTranslateOptions()
{
	string $commandName = "performMatchTranslate";
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate DefaultTemplate;
	waitCursor -state 1;
	tabLayout -tabsVisible 0 -scrollable 1;
	
	string $parent = `columnLayout -adjustableColumn 1`;
	checkBoxGrp -label (uiRes("m_performMatchTranslate.kTranslation"))
		-ncb 1
		-label1 (uiRes("m_performMatchTranslate.kAll"))
		-onc ("checkBoxGrp -e -v1 false -v2 false -v3 false matchXYZCheckBoxGrp")
		matchAllCheckBoxGrp;
		
	checkBoxGrp -ncb 3
		-label1 (uiRes("m_performMatchTranslate.kX"))
		-label2 (uiRes("m_performMatchTranslate.kY"))
		-label3 (uiRes("m_performMatchTranslate.kZ"))
		-onc ("checkBoxGrp -e -v1 false matchAllCheckBoxGrp")
		matchXYZCheckBoxGrp;

	waitCursor -state 0;
	setUITemplate -popTemplate;

	button -edit
		-label `runTimeCommand -q -label MatchTranslation`
		-command ($callback + " " + $parent + " " + 1)
		`getOptionBoxApplyBtn`;

	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		`getOptionBoxSaveBtn`;

	button -edit
		-command ($setup + " " + $parent + " " + 1)
		`getOptionBoxResetBtn`;

	setOptionBoxTitle `runTimeCommand -q -ann MatchTranslationOptions`;
	setOptionBoxHelpTag("MatchTranslation");

	eval (($setup + " " + $parent + " " + 0));
	showOptionBox();
}

proc string assembleCmd()
{
	setOptionVars(false);
	int $axisX = `optionVar -q matchTranslateX`;
	int $axisY = `optionVar -q matchTranslateY`;
	int $axisZ = `optionVar -q matchTranslateZ`;

	string $cmd = "";
	if ($axisX || $axisY || $axisZ) {
		$cmd = "matchTransform";
		if ($axisX && $axisY && $axisZ) {
			$cmd += " -pos";
		} else {
			if ($axisX) $cmd += " -px";
			if ($axisY) $cmd += " -py";
			if ($axisZ) $cmd += " -pz";
		}
	}
	return $cmd;
}

global proc string performMatchTranslate(int $action)
{
	string $cmd = "";
	switch ($action) {
	case 0: // Execute command
		$cmd = `assembleCmd`;
		if ($cmd != "") eval($cmd);
		break;
	case 1: // Options
		performMatchTranslateOptions;
		break;
	case 2: // Command string
		$cmd = `assembleCmd`;
		break;
	}
	return $cmd;
}
