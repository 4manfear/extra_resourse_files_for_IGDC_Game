// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
global int $gToggleMainWindowFullScreenModeDefer = 0;

global proc toggleMainWindowFullScreenMode()
{
	int $inFullScreenMode = `optionVar -q "workspacesInFullScreenUIMode"`;
	int $inZoomInMode = `optionVar -q "workspacesInZoomInUIMode"`;
	
	// enter full screen mode only if the zoom-in mode is not active.
	if(!$inZoomInMode) 
	{
		string $panelWithFocus = `getPanel -withFocus`;
		string $parentControl = `workspaceLayoutManager -parentWorkspaceControl $panelWithFocus`;
		int $isFloatingPanel = `workspaceControl -q -floating $parentControl`;

		if(!$isFloatingPanel) 
		{
			global int $gToggleMainWindowFullScreenModeDefer;
			if (!$gToggleMainWindowFullScreenModeDefer)
			{
				$gToggleMainWindowFullScreenModeDefer = 1;
				evalDeferred("toggleMainWindowFullScreenModeDefer " + $inFullScreenMode + " " + $parentControl);
			}
		}
	}
	else 
	{
		inViewMessage -amg (uiRes("m_toggleMainWindowFullScreenMode.kIvmZoomInModeIsActive")) -fade -pos topCenter;
	}
}

global proc toggleMainWindowFullScreenModeDefer(int $inFullScreenMode, string $parentControl)
{
	if($inFullScreenMode) 
	{
		//come out of fullscreen mode
		workspaceLayoutManager -restoreMainWindowControls;
	}
	else 
	{
		// enter fullscreen mode
		workspaceLayoutManager -collapseMainWindowControls $parentControl true;
	}
	optionVar -iv "workspacesInFullScreenUIMode" (!$inFullScreenMode);
	if(!$inFullScreenMode)
	{
		inViewMessage -amg (uiRes("m_toggleMainWindowFullScreenMode.kIvmFullScreenModeOn")) -fade -pos topCenter;
	}
			
	if (`about -macOS`){
		// We want to make sure the main window is the active window so the 
		// keyboard focus is not lost MAYA-70943
		global string $gMainWindow;
		showWindow($gMainWindow);
	}

	global int $gToggleMainWindowFullScreenModeDefer;
	$gToggleMainWindowFullScreenModeDefer = 0;
}
