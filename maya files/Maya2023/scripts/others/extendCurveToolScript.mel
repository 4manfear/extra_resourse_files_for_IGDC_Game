// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      extendCurveToolScript
//
//  Description:
//	create wrapper around the extendCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc extendCurveToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;

	optionVar -init $forceFactorySettings -category "Modeling.Curves.Extend.Extend Curve"
		-iv extendCurveEuc 0
		-iv extendCurveLac $gStartWithAutoComplete
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q extendCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q extendCurveLac` $goToTool;
	}
}

global proc extendCurveValues ( string $toolName ) 
{
	performExtendCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc extendCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createExtendCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		extendCurveToolSetup( 0, "" );
		scriptCtx -i1 "extend.png"
		  -title (uiRes("m_extendCurveToolScript.kExtendCurveTool"))
		  -bcn "extendCurve"

		  -exitUponCompletion `optionVar -q extendCurveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("extendCurveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_extendCurveToolScript.kSelectCurves"))
		  -setSelectionPrompt (uiRes("m_extendCurveToolScript.kSelectAdditionalCurves"))
		  -setDoneSelectionPrompt (uiRes("m_extendCurveToolScript.kComputeExtend"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q extendCurveLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsCurve true
		  -curveOnSurface true
		$tool;
	}
}

global proc string extendCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolExtendCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "extendCurveContext";
			createExtendCurveContext $tool;
		}
	}
	return scriptToolScript( "performExtendCurve", $setToTool, $tool );
}
