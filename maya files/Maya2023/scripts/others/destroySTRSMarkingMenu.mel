// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
global proc destroySTRSMarkingMenu(string $toolCmd)
{
	// This int will be used to signal 3 different things:
	//
	// Key was pressed for one of the T/R/S tools...
	// 0: when we were already in the Tool -> Reset the current handle.
	// 1: and then the marking menu was triggered with the mouse -> Do nothing here.
	// 2: when we were in a different Tool -> Tool was entered during press, nothing to do here. 
	//
	global int $gSTRSMarkingMenuStatus;
	if (0 == $gSTRSMarkingMenuStatus)
	{
		switch ($toolCmd)
		{
		case "MoveTool":
			if (`manipMoveContext -exists Move`)
			{
				if (`manipMoveContext -q -editPivotMode Move`)
					ctxEditMode;
				else
					manipMoveContext -edit -currentActiveHandle 3 Move;
			}
			break;
		case "RotateTool":
			if (`manipRotateContext -exists Rotate`)
			{
				if (`manipRotateContext -q -editPivotMode Rotate`)
					ctxEditMode;
				else
					manipRotateContext -edit -currentActiveHandle 3 Rotate;
			}
			break;
		case "ScaleTool":
			if (`manipScaleContext -exists Scale`)
			{
				if (`manipScaleContext -q -editPivotMode Scale`)
					ctxEditMode;
				else
					manipScaleContext -edit -currentActiveHandle 3 Scale;
			}
			break;
		default:
			break;
		}

	} 

	MarkingMenuPopDown;
}
