// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
/*
	Creation Date:  April 19, 2016
	
	Description:
	    timeEditorInViewMessage is used to display HUD messages related 
		to the time editor.
		
		The message will be displayed only if the HUD preference for 
		Time Editor is enabled.  The TimeEditor preference for HUD supersedes
		the global setting for inViewMessages.
*/

proc stripMarkupAndPrint(string $msg)
{
    string $tokens[] = {"<h1>", "</h1>", "<br>", "<hl>", "</hl>"};
    string $tmp = $msg;

    for ($t in $tokens)
        $tmp = substitute($t, $tmp, "");

    print ($tmp + "\n");
}

global proc timeEditorInViewMessage(string $msg) 
{
	if ( $msg == "" ) {
		return;
	}

	stripMarkupAndPrint($msg);
	
	if ( !`optionVar -q timeEditorHUD` ) {
		return;
	}

	int $inViewOn = `optionVar -q inViewMessageEnable`;

	// Temporarily activate in-view messaging, otherwise the time editor
	// pref would be ignored.
	if ($inViewOn == 0) {
		optionVar -iv inViewMessageEnable true;
	}
 	
	inViewMessage -smg $msg -dragKill -fade -pos topCenter;
	
	if ($inViewOn == 0) {
		optionVar -iv inViewMessageEnable false;
	}	
}
