// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  June 2000
//
//  Description:
//      Option tool property window for Artisan tools
//
//  Procedure Name:
//      artSelectProperties 
//
//  Description:
//      Initialize the option values.
//
//  Input Arguments:
//
//  Return Value:
//      None.
//

proc artSelectCreateOperFrame(
	string	$parent,
	string	$currTool
)
//
//	Description:
//		Paint Select operation menu options.
//
{
	setUITemplate -pushTemplate OptionsTemplate;
	setParent $parent;

	columnLayout;
		// Paint Select Operation.

		string $unselect			= (uiRes("m_artSelectProperties.kUnselect"));
		string $paintSelectAnnot	= (uiRes("m_artSelectProperties.kPaintSelectOperationAnnot"));	
		radioButtonGrp 
			-nrb 2
			-label (uiRes("m_artSelectProperties.kPaintOperation")) 
			-label1 (uiRes("m_artSelectProperties.kSelect"))  -label2 $unselect  
			-annotation $paintSelectAnnot 
		artSelectOperRadioButton0;

		radioButtonGrp -shareCollection artSelectOperRadioButton0
			-nrb 1
			-label  ""
			-label1 (uiRes("m_artSelectProperties.kToggle")) 
			-annotation $paintSelectAnnot 
		artSelectOperRadioButton1;

        separator;

		checkBoxGrp -ncb 1 
			-label "" 
			-label1 (uiRes("m_artSelectProperties.kAddtoCurrentSelection")) 
			-annotation (uiRes("m_artSelectProperties.kKeepAddingtoSelectionAnnot")) 
		artSelectAddToCurrentChk;

		separator;

		// Global Selection frameLayout.
		rowColumnLayout -nc 3 
				-cw 1 100 -cs 1 25
				-cw 2 100 -cs 2 15 
				-cw 3 100 -cs 3 15;

			button  -label (uiRes("m_artSelectProperties.kSelectAll"))    
					-w 100  
					-annotation (uiRes("m_artSelectProperties.kSelectAllComponentsAnnot")) 
				artSelectAllButton;
			button 	-label (uiRes("m_artSelectProperties.kUnselectAll"))  
					-w 100  
					-annotation (uiRes("m_artSelectProperties.kUnselectAllComponentsAnnot")) 
				artUnSelectAllButton;
			button 	-label (uiRes("m_artSelectProperties.kToggleAll"))    
					-w 100  
					-annotation (uiRes("m_artSelectProperties.kToggleAllComponentsAnnot")) 
				artToggleAllButton;
		setParent ..;
	setParent ..;

	setUITemplate -popTemplate;
}


// =======================================================
// Main Procedure.
// =======================================================
global proc artSelectProperties()
{

	// Base Artisan (applicable to all Artisan tools) properties.
	source "artisanProperties.mel";
	source "artisanCallback.mel";

	source "artSelectCallback.mel";

	string $currContext = `currentCtx`;
	string $currTool	= `contextInfo -c $currContext`;

	setUITemplate -pushTemplate OptionsTemplate;

	string 	  $parent = `toolPropertyWindow -q -location`;
	setParent $parent;

	columnLayout -adj true artSelect;
	
	// Brush frameLayout.
	frameLayout -label (uiRes("m_artSelectProperties.kBrush")) 
		-collapsable true -collapse false
		artSelectBrushFrame;
		
		// Create brush option menu.
		artisanCreateBrushFrame( "artSelectBrushFrame", $currTool );
	setParent ..;

	// Paint Selection operation frameLayout.
	frameLayout -label (uiRes("m_artSelectProperties.kPaintOperations")) 
		-collapsable true -collapse false 
		artSelectOperationFrame;

		// Create Paint Select operation menu.
		artSelectCreateOperFrame( "artSelectOperationFrame", $currTool );
	setParent ..;

	
	// Stroke options.
	frameLayout -label (uiRes("m_artSelectProperties.kStroke")) 
		-collapsable true -collapse true
		artSelectStrokeFrame;

		// Create stroke options.
		artisanCreateStrokeFrame( "artSelectStrokeFrame", $currTool );
	setParent ..;

	// Stylus Pressure options.
	frameLayout -label (uiRes("m_artSelectProperties.kStylusPressure")) 
		-collapsable true -collapse true
		artSelectPressureFrame;

		// Create stroke options.
		artisanCreatePressureFrame( "artSelectPressureFrame", $currTool );
	setParent ..;

	// Attribute Maps options.
	frameLayout -label (uiRes("m_artSelectProperties.kAttributeMaps")) 
		-collapsable true -collapse true
		artSelectAttrMapFrame;
	
		// Create Attribute Map option menu.
		artisanCreateAttrMapFrame( "artSelectAttrMapFrame", $currTool );
	setParent ..;
	
	// Display options.
	frameLayout -label (uiRes("m_artSelectProperties.kDisplay")) 
		-collapsable true -collapse true
		artSelectDisplayFrame;

		// Create Display options.
		artisanCreateDisplayFrame( "artSelectDisplayFrame", $currTool );
	setParent ..;

	setParent ..;	// columnLayout artSelect
	setUITemplate -popTemplate;

	// Set the callbacks and the values.
	artSelectCallback( "artSelectCtx" );
}
