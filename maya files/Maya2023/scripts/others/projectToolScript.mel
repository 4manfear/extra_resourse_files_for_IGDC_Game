// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      projectToolScript
//
//  Description:
//	create wrapper around the project command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc projectToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Project Curve on Surface"
		-iv projectCurveEuc 0
		-iv projectCurveLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q projectCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q projectCurveLac` $goToTool;
	}
}

global proc projectValues ( string $toolName ) 
{
	performProjectCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc projectProperties()
{
	scriptCtxCommonProperties;
}

proc createProjectContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		projectToolSetup( 0, "" );
		scriptCtx -i1 "projectCurve.png"
		  -title (uiRes("m_projectToolScript.kProjectCurveTool"))
		  -bcn "project"

		  -exitUponCompletion `optionVar -q projectCurveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("projectToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_projectToolScript.kSelectCurvesAndEdges")) )
		  -setSelectionPrompt ((uiRes("m_projectToolScript.kSelectMoreCurvesAndEdges")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q projectCurveLac`
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true

		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		$tool;
	}
}

global proc string projectToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolProject` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "projectCurveContext";
			createProjectContext $tool;
		}
	}
	return scriptToolScript( "performProjectCurve", $setToTool, $tool );
}
