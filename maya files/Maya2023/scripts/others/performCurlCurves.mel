// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  2003
//
//  Description:
//	     Option box for curlCurves menu.	
//


//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//
proc setOptionVars(int $forceFactorySettings)
{
	optionVar -init $forceFactorySettings -category "Dynamics.nHair.Modify Curves.Curl"
		-fv curlCurvesAmount 0.3
		-fv curlCurvesFrequency 1.0
		;
}

//
//  Procedure Name:
//      performCurlCurvesSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//		forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc performCurlCurvesSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);

	setParent $parent;

	floatSliderGrp -edit 
		-v `optionVar -query curlCurvesAmount`
		curlCurvesAmount;

	floatSliderGrp -edit 
		-v `optionVar -query curlCurvesFrequency`
		curlCurvesFrequency;

}

//
//  Procedure Name:
//      performCurlCurvesCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//		doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc performCurlCurvesCallback(string $parent, int $doIt)
{
    setParent $parent;

	optionVar -floatValue curlCurvesAmount
		`floatSliderGrp -query -v curlCurvesAmount`;

	optionVar -floatValue curlCurvesFrequency
		`floatSliderGrp -query -v curlCurvesFrequency`;

	if( $doIt ) {
		performCurlCurves 0; 
	}
}


//
//  Procedure Name:
//      curlCurvesOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc curlCurvesOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "performCurlCurves";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	string $layout = getOptionBox();
	setParent $layout;
	
	setUITemplate -pushTemplate DefaultTemplate;

	tabLayout -tv false -scr true;
	
	string $parent = `columnLayout -adjustableColumn 1`;

    floatSliderGrp
		-label (uiRes("m_performCurlCurves.kCurlAmount"))
		-field 1
		-min 0 
		-max 2.0
		-fieldMinValue -100000.0
		-fieldMaxValue 100000.0
		-pre 3
		curlCurvesAmount;

    floatSliderGrp
		-label (uiRes("m_performCurlCurves.kCurlFrequency"))
		-field 1
		-min 0.0 
		-max 10.0
		-fieldMinValue -100000.0
		-fieldMaxValue 100000.0
		-pre 3
		curlCurvesFrequency;


	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performCurlCurves.kCurlCurves"))
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Set the option box title.
	//
	setOptionBoxTitle (uiRes("m_performCurlCurves.kCurlCurvesOptions"));
	setOptionBoxCommandName($commandName);
	setOptionBoxHelpTag( "CurlCurves" );

	// Pop the UI template
	//
	setUITemplate -popTemplate;

	//	Set the current values of the option box.
	//
	eval( $setup + " " + $parent + " " + 0 );	
	
	//	Show the option box.
	//
	showOptionBox();
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc string assembleCmd()
{
	setOptionVars(false);

	string $cmd = ("modifySelectedCurves curl " 
					+ `optionVar -query curlCurvesAmount` +  " "
					+ `optionVar -query curlCurvesFrequency` );

	return $cmd;
}

//
//  Procedure Name:
//      performCurlCurves
//
//  Description:
//		Perform the command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the command with the current 
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performCurlCurves(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command from option settings.
		//
		case 0:
			setOptionVars(false);
			$cmd = `assembleCmd`;
			eval($cmd);
			break;

		//	Show the option box.
		//
		case 1:
			curlCurvesOptions();
			break;

		//	Return the command string.
		//
		case 2:
			setOptionVars (false);
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}

