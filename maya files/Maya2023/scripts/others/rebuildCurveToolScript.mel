// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      rebuildCurveToolScript
//
//  Description:
//	create wrapper around the rebuildCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc rebuildCurveToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Curves.Rebuild"
		-iv rebuildCurveEuc 0
		-iv rebuildCurveLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q rebuildCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q rebuildCurveLac` $goToTool;
	}
}

global proc rebuildCurveValues ( string $toolName ) 
{
	performRebuildCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc rebuildCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createRebuildCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		rebuildCurveToolSetup( 0, "" );
		scriptCtx -i1 "rebuildCurve.png"
		  -title (uiRes("m_rebuildCurveToolScript.kRebuildCurveDirectionTool"))
		  -bcn "rebuildCurve"

		  -exitUponCompletion `optionVar -q rebuildCurveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("rebuildCurveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_rebuildCurveToolScript.kSelectCurves"))
		  -setSelectionPrompt (uiRes("m_rebuildCurveToolScript.kSelectAdditionalCurves"))
		  -setDoneSelectionPrompt (uiRes("m_rebuildCurveToolScript.kRebuildCurve"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q rebuildCurveLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsCurve true
		  -curveOnSurface true
		$tool;
	}
}

global proc string rebuildCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolRebuildCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "rebuildCurveContext";
			createRebuildCurveContext $tool;
		}
	}
	return scriptToolScript( "performRebuildCurve", $setToTool, $tool );
}
