// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      extendSurfaceToolScript
//
//  Description:
//	create wrapper around the extendSurface command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc extendSurfaceToolSetup( int $forceFactorySettings,
									 string $goToTool )
{
	global int $gStartWithAutoComplete;

	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Extend"
		-iv extendSurfaceEuc 0
		-iv extendSurfaceLac $gStartWithAutoComplete
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q extendSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q extendSurfaceLac` $goToTool;
	}
}

global proc extendSurfaceValues ( string $toolName ) 
{
	performExtendSurface 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc extendSurfaceProperties()
{
	scriptCtxCommonProperties;
}

proc createExtendSurfaceContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		extendSurfaceToolSetup( 0, "" );
		scriptCtx -i1 "extend.png"
		  -title (uiRes("m_extendSurfaceToolScript.kExtendSurfaceTool"))
		  -bcn "extendSurface"

		  -exitUponCompletion `optionVar -q extendSurfaceEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("extendSurfaceToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_extendSurfaceToolScript.kSelectSurfaceOrIsoparm")) )
		  -setSelectionPrompt ((uiRes("m_extendSurfaceToolScript.kSelectMoreSurfaces")) )
		  -setDoneSelectionPrompt ((uiRes("m_extendSurfaceToolScript.kPressEnterToComplete")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q extendSurfaceLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true
		  -isoparm true
		$tool;
	}
}

global proc string extendSurfaceToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolExtendSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "extendSurfaceContext";
			createExtendSurfaceContext $tool;
		}
	}
	return scriptToolScript( "performExtendSurface", $setToTool, $tool );
}

