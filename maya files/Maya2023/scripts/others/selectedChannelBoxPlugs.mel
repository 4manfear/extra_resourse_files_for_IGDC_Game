// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//	Procedure Name:
//		selectedChannelBoxPlugs()
//
//	Description:
//		Returns the list of all plugs (node + attributes) that are
//		currently selected in the channel box.  If no
//		attributes are selected, the returned string[] is
//		empty.
//
//  Input Arguments:
//		None.
//
//  Return Value:
//      String array of object plugs
//

global proc string[] selectedChannelBoxPlugs()
{
	global 	string $gChannelBoxName; 

	string  $result[];

	if( `channelBox -q -exists $gChannelBoxName` ) {
		// The channel box now supports showing UFE attributes, so first check if the
		// item being displayed is non-Maya. If yes return nothing as non-Maya doesn't
		// have plugs.
		string	$mainObjects[] = `channelBox -q -mainObjectList $gChannelBoxName`;
		if (size($mainObjects) > 0) {
			string $firstObj = $mainObjects[0];
			if (! `objExists $firstObj`) {
				int $nonMaya = python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.isPathStringValidNonMayaObject('" + $firstObj + "')");
				if ($nonMaya) {
					return $result;
				}
			}
		}

		string 	$main[];
		string 	$shape[], $shapeObjects[];
		string 	$history[], $historyObjects[];
		string  $outputs[], $outputObjects[];
		
		$main = `channelBox -q -selectedMainAttributes $gChannelBoxName`;
		$shape = `channelBox -q -selectedShapeAttributes $gChannelBoxName`;
		$history = `channelBox -q -selectedHistoryAttributes $gChannelBoxName`;
		$outputs = `channelBox -q -selectedOutputAttributes $gChannelBoxName`;
		
		string  $attr;
		
		$main = `channelBox -q -selectedMainAttributes $gChannelBoxName`;

		$shape = `channelBox -q -selectedShapeAttributes $gChannelBoxName`;
		$shapeObjects = `channelBox -q -shapeObjectList $gChannelBoxName`;

		$history = `channelBox -q -selectedHistoryAttributes $gChannelBoxName`;
		$historyObjects = `channelBox -q -historyObjectList $gChannelBoxName`;

		$outputs = `channelBox -q -selectedOutputAttributes $gChannelBoxName`;
		$outputObjects = `channelBox -q -outputObjectList $gChannelBoxName`;
		
		if( size( $main ) > 0 ) {
			for( $object in $mainObjects ) {
				for ($mattr in $main) {
					$result[ size($result) ] = ($object+"."+$mattr);
				}
			}
		}

		if( size( $shape ) > 0 ) {
			for( $object in $shapeObjects ) {
				for ($mattr in $shape) {
					$result[ size($result) ] = ($object+"."+$mattr);
				}
			}
		}

		if( size( $history ) > 0 ) {
			for( $object in $historyObjects ) {
				for ($mattr in $history) {
					$result[ size($result) ] = ($object+"."+$mattr);
				}
			}
		}

		if( size( $outputs ) > 0 ) {
			for( $object in $outputObjects ) {
				for ($mattr in $outputs) {
					$result[ size($result) ] = ($object+"."+$mattr);
				}
			}
		}
	}
		
	return $result;
}
