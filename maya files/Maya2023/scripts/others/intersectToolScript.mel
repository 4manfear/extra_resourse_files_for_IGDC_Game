// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      intersectToolScript
//
//  Description:
//	create wrapper around the intersect command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc intersectToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Intersect"
		-iv intersectEuc 0
		-iv intersectLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q intersectEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q intersectLac` $goToTool;
	}
}

global proc intersectValues ( string $toolName ) 
{
	performIntersect 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc intersectProperties()
{
	scriptCtxCommonProperties;
}

proc createIntersectContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		intersectToolSetup( 0, "" );
		int $n = 0;
		if( `optionVar -q intersectLac` ) {
			$n = 2;
		}
		scriptCtx -i1 "srfIntersect.png"
		  -title (uiRes("m_intersectToolScript.kIntersectSurfacesTool"))
		  -bcn "intersect"

		  -exitUponCompletion `optionVar -q intersectEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("intersectToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_intersectToolScript.kSelectNurbsSurface")) )
		  -setSelectionPrompt ((uiRes("m_intersectToolScript.kSelectMoreSurfaces")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q intersectLac`
		  -showManipulators true

		  -setSelectionCount $n
		  -nurbsSurface true
		$tool;
	}
}

global proc string intersectToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolIntersect` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "intersectContext";
			createIntersectContext $tool;
		}
	}
	return scriptToolScript( "performIntersect", $setToTool, $tool );
}
