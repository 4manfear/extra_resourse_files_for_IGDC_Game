// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
// Return a string array containing the deformers whose weights can be
// exported. First in the array are any deformers on the currently selected
// objects(s). Then all the rest of the deformers are listed.
//
global
proc string[] findExportableDeformers()
{
	string $dfArray[];
	string $history[] = `listHistory`;
	for ($obj in $history) {
		string $handledType[] = `ls -type wire -type blendShape -type weightGeometryFilter -type skinCluster $obj`;
		if (size($handledType) > 0) {
			$dfArray[size($dfArray)] = $obj;
		}
	}
	int $dfCount = size($dfArray);
	
	string $allDefs[] = `ls -type wire -type blendShape -type weightGeometryFilter -type skinCluster`;
	for ($obj in $allDefs) {
		int $found = 0;
		for ($ii = 0; $ii < $dfCount; $ii++) {
			if ($dfArray[$ii] == $obj) {
				$found = 1;
				break;
			}
		}
		if (! $found) {
			$dfArray[size($dfArray)] = $obj;
		}
	}
	return $dfArray; 
}

