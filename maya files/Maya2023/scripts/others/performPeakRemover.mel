// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  November 9, 2017
//
//  Description:
//      This is a helper script to perform the filterCurve -f peakRemover
//	command using the various options that have been set.
//
//  Input Arguments:
//		int action		0 - just execute the command
//						1 - show the option box dialog
//						2 - return the drag command
//
//  Return Value:
//      None.
//

proc setOptionVars (int $forceFactorySettings)
{
	string $category = "Graph Editor.Peak Removal Filter";

	keySetOptionBoxCommon( { "peakRemover", 
							 "unknown",
							 "setOptionVars", 
							 $forceFactorySettings,
							 $category,
							 /*all anim curves*/0,
							 /*selected time range*/4 } );
}

global proc peakRemoverSetup (string $parent, string $selectionConnection, int $forceFactorySettings)
{
	// Retrieve the option settings
	//
	setOptionVars ($forceFactorySettings);

	setParent $parent;

	keySetOptionBoxCommon( { "peakRemover", $selectionConnection, "setup", 0 } );

	peakRemoverWidgetsEnable ($selectionConnection);
}


global proc peakRemoverCallback( string $parent, int $doIt,
							  string $selectionConnection, int $performAction )
{
    setParent $parent;

	keySetOptionBoxCommon( { "peakRemover", $selectionConnection, "callback" } );

	if( $doIt )
	{
		if( peakRemoverPreviewToolActive() )
		{
			CompleteCurrentTool();
		}
		else
		{
			performPeakRemover( $performAction, $selectionConnection);
			string $tmpCmd = "performPeakRemover ";
			$tmpCmd += "\"" + $performAction + "\", ";
			$tmpCmd += "\"" + $selectionConnection + "\"";
			addToRecentCommandQueue $tmpCmd "PeakRemover";
		}
	}
}

// Interactive preview support
global proc int peakRemoverPreviewToolActive()
//
//	Description:
//		Returns 1 if the preview tool is active, 0 otherwise.
//
{
	string $tool = filterPeakRemoverTool();
	return (`currentCtx` == $tool);
}

global proc peakRemoverPreviewInit( int $state )
//
//	Description:
//		Initialize/uninitialize the preview tool depending
//		on $state. This initialization is responsible for
//		installing change callbacks on the widgets to invoke
//		the ($cmd + "PreviewUpdate") callback and update UI
//		enable states.
//
{
	keySetOptionPreviewInit( "peakRemover", $state );

	string $applyBtn = getOptionBoxApplyBtn();
	int $applyEnable = !$state;
	button -e -enable $applyEnable $applyBtn;
}

global proc peakRemoverPreviewEnable()
//
//	Description:
//		This callback is invoked when the preview option widget
//		is checked on. Initialize the preview tool and set it
//		as the active tool context.
//
{
	peakRemoverPreviewInit(1);
	peakRemoverPreviewUpdate();

	// Enable filterPeakRemoverCtx
	string $tool = filterPeakRemoverTool();
	if( `currentCtx` != $tool )
	{
		setToolTo $tool;
	}
}

global proc peakRemoverPreviewUpdate()
//
//	Description:
//		This callback is invoked when preview relevant widget
//		values change. This update call is responsible for
//		updating the tool context with the tool parameters.
//
{
	string $tool = filterPeakRemoverTool();

	// Update the optionVars
	global string $gOptionBox;
	peakRemoverCallback( $gOptionBox, false, "", 0 );

	// Update filterPeakRemoverCtx from optionVars
	int $whichRange = `optionVar -q peakRemoverWhichRange`;
	string $range = `optionVar -q peakRemoverRange`;

	float $startTime = `playbackOptions -q -min`;
	float $endTime = `playbackOptions -q -max`;
	int $selectedKeys = 0;

	if( $whichRange == 4 ) {
		$selectedKeys = 1;
	} else if( $whichRange > 2 ) {
		string $buff[];
		tokenize $range ":" $buff;
		$startTime = $buff[0];
		$endTime = $buff[1];
	}

	string $cmd = "filterPeakRemoverCtx -e";
	$cmd += " -startTime ";
	$cmd += $startTime;
	$cmd += " -endTime ";
	$cmd += $endTime;
	$cmd += " -selectedKeys ";
	$cmd += $selectedKeys;
	$cmd += (" " + $tool);
	eval($cmd);
}

global proc peakRemoverPreviewDisable()
//
//	Description:
//		This callback is invoked when the preview option widget
//		is checked off. Uninitialize the preview tool and cancel
//		the tool context.
//
{
	string $tool = filterPeakRemoverTool();
	if( `currentCtx` == $tool )
	{
		escapeCurrentTool();
	}

	peakRemoverPreviewInit(0);
}

global proc peakRemoverWidgetsEnable(string $selectionConnection)
{
	keySetOptionBoxCommon( { "peakRemover", $selectionConnection, "enable" } );	

	int $previewActive = peakRemoverPreviewToolActive();
	string $applyBtn = getOptionBoxApplyBtn();
	int $applyEnable = !$previewActive;
	button -e -enable $applyEnable $applyBtn;
}

proc string peakRemoverWidgets( string $tabLayout, string $selectionConnection )
{
	setParent $tabLayout;

	string $tabForm = `columnLayout -adjustableColumn true`;

	keySetOptionBoxCommon( { "peakRemover", $selectionConnection, "widgets", 1, 0, 1, 1, 1, 1 } );

	int $previewActive = peakRemoverPreviewToolActive();
	peakRemoverPreviewInit($previewActive);

	return $tabForm;
}

global proc peakRemoverDoSelectionChanged(string $selectionConnection)
//
// Description:
//	Since timeRange is irrelevant when there are 
//	picked keyframes, collapse the timeRange
//	when there are.  (This is hooked in to a SelectionChanged
//	trigger.)
//
{
	keySetOptionBoxCommon( { "peakRemover", $selectionConnection, "selectionChanged" } );
	peakRemoverWidgetsEnable ($selectionConnection);
}


proc peakRemoverOptions( string $selectionConnection, int $performAction )
{
	// Customisation options
	//
	// Name of the command for this option box (think of it as the base class)
	string $commandName = "peakRemover";
	// Title for the option box window
	string $optionBoxTitle = (uiRes("m_performPeakRemover.kPeakRemoverCurveOptions"));
	// Title for the apply button
	string $applyTitle = (uiRes("m_performPeakRemover.kApplyAndClose"));

	// Build the option box "methods"
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	// Build the window, with a tab layout
	//
	string $widgetList[] = `getStandardWindow $optionBoxTitle 0 "noOptions"`;
	setUITemplate -pushTemplate DefaultTemplate;

	// Make the form invisible while we create the widgets in the window
	//
	formLayout -e -vis false $widgetList[1];

	// Attach each tab
	//
	peakRemoverWidgets $widgetList[2] $selectionConnection;

	// Attach the standard buttons
	//
	string $buttonList[] = `addStandardButtons $commandName $applyTitle 
		$widgetList[1] $widgetList[2] "noOptions"`;

	// attach commands to the standard buttons
	//
	// Save
	//
	button -e -c ($callback + " " + $widgetList[0] + " false \"\" " + $performAction +
				  "; hideOptionBox()") $buttonList[3];

	// Close
	//
	button -edit -label (uiRes("m_performPeakRemover.kCancel")) -command ("peakRemoverPreviewDisable; hideOptionBox") $buttonList[2];

	// Reset
	//
	button -edit -command ($setup + " " + $widgetList[0] + " " + $selectionConnection + " true") $buttonList[1];

	// Do It
	//
	button -edit -command ($callback + " " + $widgetList[0] + " true \"" +
						   $selectionConnection + "\"" + $performAction) $buttonList[0];

	// Preview close callback
	keySetOptionPreviewOnClose( "peakRemoverPreviewDisable" );

	// Make the form layout visible so we can see what we built, and
	// reset the template
	//
	formLayout -e -vis true $widgetList[1];
	setUITemplate -popTemplate;

	//	Customize the 'Help' menu item text.
	//	This is called from 3 different places so look at the selection
	//	connection to try to figure out which one.
	//
	if (match ("graphEditor", $selectionConnection) != "") {
		setOptionBoxHelpTag( "GraphPeakRemoverCurve" );
	} else if (match ("dopeSheet", $selectionConnection) != "") {
		setOptionBoxHelpTag( "DopePeakRemoverCurve" );
	} else {
		setOptionBoxHelpTag( "" );
	}

	// Call the setup "method" to fill in the current settings
	//
	eval (($setup + " " + $widgetList[0] + " " + $selectionConnection + " false"));	
	showOptionBox();
    showWindow $widgetList[0];

	peakRemoverDoSelectionChanged($selectionConnection);	
	scriptJob -protected 
		-event "SelectionChanged" ("peakRemoverDoSelectionChanged " + $selectionConnection) 
		-parent $widgetList[2];
}

proc string assembleCmd( string $selectionConnection, string $options ) 
{
	string $cmd = "doPeakRemoverArgList 1 { " + 
				"\"" + `optionVar -q peakRemoverWhichRange` + "\"" +
				",\"" + `optionVar -q peakRemoverRange` + "\"" +
				",\"" + $selectionConnection + "\"" +
				",\"" + $options + "\"" +	
				" };";
	return $cmd;
}

// The action variable means
//		0 - do the command
//		1 - show the option box
//		2 - return the drag command
//		3 - do the command (bufferCurve version)
//		4 - show the option box (bufferCurve version)
//		5 - return the drag command (bufferCurve version)
global proc string performPeakRemover( int $action, string $selectionConnection )
{
	string $cmd = "";

	switch( $action ) {
	case 0:
		setOptionVars( false );
		$cmd = assembleCmd( $selectionConnection, "noOptions" );
		eval( $cmd );
		break;
	case 1:
		peakRemoverOptions( $selectionConnection, 0 );
		break;
	case 2:
		setOptionVars( false );
		$cmd = assembleCmd( $selectionConnection, "noOptions" );
		break;
	case 3:
		setOptionVars( false );
		$cmd = assembleCmd( $selectionConnection, "bufferCurve" );
		eval( $cmd );
		break;
	case 4:
		peakRemoverOptions( $selectionConnection, 3 );
		break;
	case 5:
		setOptionVars( false );
		$cmd = assembleCmd( $selectionConnection, "bufferCurve" );
		break;
	}
	return ($cmd);
}
