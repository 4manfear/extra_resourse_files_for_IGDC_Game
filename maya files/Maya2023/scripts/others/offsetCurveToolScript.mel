// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      offsetCurveToolScript
//
//  Description:
//	create wrapper around the offsetCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc offsetCurveToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;

	optionVar -init $forceFactorySettings -category "Modeling.Curves.Offset.Offset Curve"
		-iv offsetCurveEuc 0
		-iv offsetCurveLac $gStartWithAutoComplete
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q offsetCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q offsetCurveLac` $goToTool;
	}
}

global proc offsetCurveValues ( string $toolName ) 
{
	performOffsetCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc offsetCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createOffsetCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		offsetCurveToolSetup( 0, "" );
		scriptCtx -i1 "offsetCurve.png"
		  -title (uiRes("m_offsetCurveToolScript.kOffsetCurveTool"))
		  -bcn "offsetCurve"

		  -exitUponCompletion `optionVar -q offsetCurveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("offsetCurveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_offsetCurveToolScript.kSelectCurves"))
		  -setSelectionPrompt (uiRes("m_offsetCurveToolScript.kSelectAdditionalCurves"))
		  -setDoneSelectionPrompt (uiRes("m_offsetCurveToolScript.kComputeOffset"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q offsetCurveLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		$tool;
	}
}

global proc string offsetCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolOffsetCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "offsetCurveContext";
			createOffsetCurveContext $tool;
		}
	}
	return scriptToolScript( "performOffsetCurve", $setToTool, $tool );
}
