// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================

global string $gBatchRenderOverrideMenu = "batchRenderOverideMenu";

// Update the state of the Render Override menu

global proc updateRenderOverride()
{
    global string $gBatchRenderOverrideMenu;

    // Check existance
    if ( !`optionMenuGrp -q -exists $gBatchRenderOverrideMenu`)
    {
        return;
    }

    // Save current parent
    string $oldParent = `setParent -query`;
    // Set override menu as parent
    setParent -menu ($gBatchRenderOverrideMenu + "|OptionMenu");

    // Remove all the existing menu items
    string $existingMenuItems[] = `optionMenuGrp -q -itemListLong $gBatchRenderOverrideMenu`;
    string $menuItem;
    for( $menuItem in $existingMenuItems )
    {
        deleteUI -menuItem $menuItem;
    }

    // Build the menu
    // Make first menu item as "no render item" and select it
    menuItem -label (uiRes("m_updateRenderOverride.kNoRenderOverride"));
    optionMenuGrp -edit -select 1 $gBatchRenderOverrideMenu;

    // Create menu items with valuse queried from the ogsRender
    string $override;
    string $overrides[] = `ogsRender -q -availableRenderOverrides`;
    for( $override in $overrides )
    {
        menuItem -label $override;
    }

    // Select the menu item with the active render override
    string $activeOverride = `ogsRender -q -activeRenderOverride`;
    if( $activeOverride != "" )
    {
        // If there is valid render override, select it
        optionMenuGrp -edit -value $activeOverride $gBatchRenderOverrideMenu;
    }

    // Register selection command
    optionMenuGrp -edit -changeCommand "activateRenderOverride" $gBatchRenderOverrideMenu;

    setParent $oldParent;
}

global proc activateRenderOverride()
{
    global string $gBatchRenderOverrideMenu;
    // Check existance
    if ( !`optionMenuGrp -q -exists $gBatchRenderOverrideMenu`)
    {
        return;
    }

    string $selectedOverride = `optionMenuGrp -q -value $gBatchRenderOverrideMenu`;
    if( $selectedOverride == (uiRes("m_updateRenderOverride.kNoRenderOverride")) )
    {
        $selectedOverride = "";
    }
    ogsRender -e -activeRenderOverride $selectedOverride;
}


