// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Description:
//		This script sets the values for the specified latticeDeformKey
//	context within the tool property sheet. 
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//

global proc latticeDeformKeysRowChange( string $parent, string $context )
{
	setParent $parent;
	int $row = `intSliderGrp -query -value latticeDeformRows`;
	latticeDeformKeyCtx -edit -latticeRows $row $context;
}

global proc latticeDeformKeysColumnChange( string $parent, string $context )
{
	setParent $parent; 
	int $col = `intSliderGrp -query -value latticeDeformColumns`;
	latticeDeformKeyCtx -edit -latticeColumns $col $context;
}

global proc latticeDeformKeysEnvelopeChange( string $parent, string $context )
{
	setParent $parent;
	float $env = `floatSliderGrp -query -value latticeDeformEnvelope`;
	latticeDeformKeyCtx -edit -envelope $env $context;
}

global proc latticeDeformKeysScaleChange( string $parent, string $context )
{
	setParent $parent;
	int $env = `checkBoxGrp -query -value1 latticeDeformScale`;
	latticeDeformKeyCtx -edit -slp $env $context;
}

global proc latticeDeformKeysValues( string $context ) 
{
	string $parent = `toolPropertyWindow -query -location`;
	setParent $parent;
	
	int $rows = `latticeDeformKeyCtx -query -latticeRows $context`;
	int $col  = `latticeDeformKeyCtx -query -latticeColumns $context`;
	float $env = `latticeDeformKeyCtx -query -envelope $context`;
	int $sc = `latticeDeformKeyCtx -query -slp $context`; 
	
	intSliderGrp -edit 
		-changeCommand ("latticeDeformKeysRowChange " + $parent + " " + $context)
		-value $rows
		latticeDeformRows;

	intSliderGrp -edit
		-changeCommand ("latticeDeformKeysColumnChange " + $parent + " " + $context)
		-value $col
		latticeDeformColumns;

	floatSliderGrp -edit
		-changeCommand ("latticeDeformKeysEnvelopeChange " + $parent + " " + $context)
		-value $env
		latticeDeformEnvelope;

	checkBoxGrp -edit 
		-changeCommand ("latticeDeformKeysScaleChange " + $parent + " " + $context)
		-value1 $sc
		latticeDeformScale;

	string $helpTag = "LatticeDeformKeyTool";
	toolPropertySetCommon $context "latticeDeformKeySmall.png" $helpTag; 
	toolPropertySelect "latticeDeformKeys";
}
