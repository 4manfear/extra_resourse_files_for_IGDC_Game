// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  Mar 14, 1997
//
//  Description:
//      This script runs rebuild curve on the selection list.
//


global proc fitBsplinePreset( int $doHistory, float $globalTol,
							  int $crvUseGlobalTol, float $crvLocalTol )
{
	// Get a list of each type of acceptable object type - 
	// curves, and curves-on-surface.
	//
    global int $gSelectNurbsCurvesBit;
    global int $gSelectIsoparmsBit;
    global int $gSelectCurvesOnSurfacesBit;
    global int $gSelectSurfaceEdgeBit;
	global int $gSelectMeshEdge;


	string $curveList[] = `filterExpand -ex true -sm $gSelectMeshEdge -sm $gSelectNurbsCurvesBit -sm $gSelectIsoparmsBit -sm $gSelectCurvesOnSurfacesBit -sm $gSelectSurfaceEdgeBit`;

	if( size($curveList) <= 0 ) {
		string $msg = (uiRes("m_fitBsplinePreset.kSelectError"));
		error($msg);
		return;
	}

	// Execute rebuildCurve on all active curves.
	//
	$cmd = "fitBspline" + " -ch " + $doHistory;
	if( $crvUseGlobalTol == 0 ) {
		$cmd += " -tol " + $globalTol;
	}
	else {
		$cmd += " -tol " + $crvLocalTol;
	}
	$cmd += " %s;";
	string $curveResults[] = executeForEachObject( $curveList, $cmd );

	if( size($curveResults) == 0 ) {
		string $msg = (uiRes("m_fitBsplinePreset.kFailure"));
		error($msg);
	}
	else {
		// Select all the results with one select command.  Note that only
		// resulting surfaces and curves are selected, not dependency nodes.
		//
		string $selectString;
		$selectString = "select ";
		int $i;

		int $numCurves = size($curveResults);
		for( $i = 0; $i < $numCurves; $i ++ ) {
			$selectString += $curveResults[$i];
			$selectString += " ";
		}
		$selectString += ";";
		select -cl;
		eval($selectString);
	}
}

