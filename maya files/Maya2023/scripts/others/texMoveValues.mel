// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================

global proc texMoveValues ( string $toolName ) {

	string $parent = (`toolPropertyWindow -q -location` + "|texMove");
	setParent $parent; 

	texTranConstUpdate("texTranConstTextField");

	menu -e -dai texMoveSnapPopupMenu;
	menuItem -label (uiRes("m_texMoveValues.kOff")) -p texMoveSnapPopupMenu -c ("texMoveContext -e -snap 0 " + $toolName);
	menuItem -label (uiRes("m_texMoveValues.kOn")) -p texMoveSnapPopupMenu -c ("texMoveContext -e -snap 1 " + $toolName);
	menu -e -pmc("texMoveContext -e -snap (!`texMoveContext -q -snap " + $toolName + "`) " + $toolName) texMoveSnapTogglePopupMenu;
	
	string $label = ""; 
	int $snapMode = `texMoveContext -q -snap $toolName`;
	if($snapMode) 
		$label = uiRes("m_texMoveValues.kOn");
	else 
		$label = uiRes("m_texMoveValues.kOff");
 
	textField -e -tx $label texMoveSnapModeLabel; 
	floatField -e -en ($snapMode != 0) -v (`texMoveContext -q -snapValue $toolName`) texMoveSnapValue;

	floatField -e -cc("texMoveContext -e -snapValue #1 " + $toolName) texMoveSnapValue;
					
	int $isSnapCompRelative = `texMoveContext -q -snapComponentsRelative $toolName`;
	checkBoxGrp -e -v1 $isSnapCompRelative texMoveSnapCompRelative;
	
	checkBoxGrp -e 
		-cc1 ("texMoveContext -e -snapComponentsRelative #1 " + $toolName)  
		texMoveSnapCompRelative;

	// Update soft select values
	reflectionValues( );
	softSelectValues( $parent );
	commonSelectValues( $parent );

	string $icon = "moveUVShell.png";
	string $helpTag = "TexMoveTool";
	toolPropertySetCommon $toolName $icon $helpTag;
	toolPropertySelect "texMove";
}
