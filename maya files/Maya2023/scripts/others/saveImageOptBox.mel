// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
proc createOptionsUI()
{
	setUITemplate -pushTemplate DefaultTemplate;
	waitCursor -state 1;
        
	string $parent = `columnLayout -adjustableColumn 1`;

    radioButtonGrp -nrb 1 -label (uiRes("m_saveImageOptBox.kSaveMode")) 
		-label1 (uiRes("m_saveImageOptBox.kSaveRaw"))
		renderViewSaveModeRadio;

	radioButtonGrp -nrb 1
		-label1 (uiRes("m_saveImageOptBox.kSaveViewColorManaged"))
		-shareCollection renderViewSaveModeRadio
		renderViewSaveModeRadio2;

	radioButtonGrp -nrb 1
		-label1 (uiRes("m_saveImageOptBox.kSaveOutputColorManaged"))
		-shareCollection renderViewSaveModeRadio
        -enable `colorManagementPrefs -query -outputTransformEnabled`
		renderViewSaveModeRadio3;

    int $mode = 0;
    if( `optionVar -exists renderViewSaveMode` )
    {
        $mode = `optionVar -q renderViewSaveMode`;
    }
    
    if( $mode==2 && !`colorManagementPrefs -query -outputTransformEnabled` )
    {
        $mode = 0;
        optionVar -intValue renderViewSaveMode $mode;
    }

    switch ($mode)
    {
    case 2:
	    radioButtonGrp -e -select 1 renderViewSaveModeRadio3;
	    break;
    case 1:
	    radioButtonGrp -e -select 1 renderViewSaveModeRadio2;
	    break;
    default:
	    radioButtonGrp -e -select 1 renderViewSaveModeRadio;
	    break;
    }

	waitCursor -state 0;
	setUITemplate -popTemplate;
}

global proc saveImageOptionsCreate(string $parent)
{
	setParent $parent;
	$parent = `scrollLayout -childResizable true`;

	createOptionsUI();
}

global proc setRenderViewSaveMode(string $parent)
{
	setParent $parent;

	if (`radioButtonGrp -q -select renderViewSaveModeRadio3`)
		optionVar -intValue renderViewSaveMode 2;
    else
    {
        if (`radioButtonGrp -q -select renderViewSaveModeRadio2`)
            optionVar -intValue renderViewSaveMode 1;
        else 
            optionVar -intValue renderViewSaveMode 0;
    }
}

global proc resetRenderViewSaveMode(string $parent)
{
	setParent $parent;
	optionVar -intValue renderViewSaveMode 0;
	radioButtonGrp -e -select 1 renderViewSaveModeRadio;
}

global proc saveImageOptBox ()
{
	string $layout = getOptionBox();
	setParent $layout;

	createOptionsUI();

	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -command ("setRenderViewSaveMode " + $layout)
		$applyBtn;
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit -command ("setRenderViewSaveMode " + $layout)
		$saveBtn;
	string $resetBtn = getOptionBoxResetBtn();
	button -edit -command ("resetRenderViewSaveMode " + $layout)
		$resetBtn;
                         
	setOptionBoxTitle (uiRes("m_saveImageOptBox.kRenderViewSaveOptions"));

	showOptionBox();
}
