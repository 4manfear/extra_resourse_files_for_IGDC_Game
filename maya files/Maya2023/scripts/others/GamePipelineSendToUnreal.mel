// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//  Description:
//      Export Asset to Unreal project
//

global proc GamePipelineSendToUnreal(int $exportSelectionOnly)
{
    global string $gDirRetainingOptionVar; 

    string $unrealAssetRoot = `optionVar -query "UnrealProject"` + "/Import";
    sysFile -makeDir $unrealAssetRoot;
    
    string $workspace = `workspace -q -fullName`; 
    loadPlugin -quiet fbxmaya;
    setWorkingDirectory $workspace "mayaBinary" "export";

    // cache current working directory
    $currentDir = `optionVar -q $gDirRetainingOptionVar`;

    // use Unreal asset root as the current working directory
    // or use the previous visited Unreal asset folder as the current working directory
    string $unrealPath = $unrealAssetRoot;
    if (`optionVar -exists "UnrealAsset"`)
    {
        string $unrealAssetDir = `optionVar -query "UnrealAsset"`;
        if (`startsWith $unrealAssetDir $unrealAssetRoot`)
        {
            $unrealPath = $unrealAssetDir;
        }    
    }

    retainWorkingDirectory($unrealPath);

	//	Default to FBX export unless the user has chosen something else.
	string $unrealExportType = "FBX export";
	string $normalExportType = "";

	if (`optionVar -exists UnrealExportFileType`)
	{
		$unrealExportType = `optionVar -q UnrealExportFileType`;
	}

	//	The Export dialogs get their default file type from optionVars. We
	//	temporarily override the relevant var to use our preferred type for
	//	Unreal.
	string $normalExportVar = "defaultFileExportAllType";

	if ( $exportSelectionOnly )
	{
		$normalExportVar = "defaultFileExportActiveType";
	}

	if (`optionVar -exists $normalExportVar`)
	{
		$normalExportType = `optionVar -q $normalExportVar`;
	}

	optionVar -sv $normalExportVar $unrealExportType;

    if ( $exportSelectionOnly )
    {
        ExportSelection;
    }
    else
    {
        Export;
    }

	//	The normal export optionVar will now contain whatever file type the user
	//	selected. Let's move that into the Unreal-specific optionVar.
	$unrealExportType = `optionVar -q $normalExportVar`;
	optionVar -sv UnrealExportFileType $unrealExportType;

	//	Restore the original value/state of the normal optionVar.
	if ($normalExportType != "")
	{
		optionVar -sv $normalExportVar $normalExportType;
	}
	else
	{
		optionVar -rm $normalExportVar;
	}

    // store the current asset folder
    string $currentAssetDir = `optionVar -q $gDirRetainingOptionVar`;
    optionVar -stringValue "UnrealAsset" $currentAssetDir;

    // restore the workspace directory
    retainWorkingDirectory($currentDir);
}
