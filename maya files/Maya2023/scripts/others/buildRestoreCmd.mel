// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
// Description:
//		Build, and optionally execute, a command to restore the selected
//	keyframes, excluding speficied curves.
//
// Inputs:
//		excludedCurves:	List of curves to exclude from selection
//		execute:		If true, execute the resulting command
//
// Outputs:
//		string:			Returns the command to perform the selection
//
global
proc string buildRestoreCmd( string $excludedCurves[], int $execute )
{
	string $restoreCmd;
	string $selectedCurves[] = `keyframe -query -sl -name`;
	for ($curve in $selectedCurves) {
		if( stringArrayContains( $curve, $excludedCurves ) ){
			// Skip this curve
			//
			continue;
		}
		float $curveKeys[] = `keyframe -q -sl $curve`;
		int $keyCount = size($curveKeys);
		if( $keyCount > 0 ){
			$restoreCmd += "selectKey -add -k";
			int $i = 0;
			while( $i < $keyCount ){
				$restoreCmd += (" -t " + $curveKeys[$i]);
				$i++;
			}
			$restoreCmd += (" " + $curve + ";");
		}
	}
	if( $execute ){
		eval( ("selectKey -clear;" + $restoreCmd) );
	}
	return $restoreCmd;
}

