// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//<doc>
//<name intArrayRemoveAtIndex>
//
//<synopsis>
//		int intArrayRemoveAtIndex(int $index, int[] $list)
//
//<description>
//		Remove the entry at $index from the int array $list.
//
//<flags>
//		int   $index  The index of the element to remove from $list.
//		int[] $list   A list of int values.
//
//<returns>
//		int : Returns false if $index was out of range and true on
//			  success.
//
//<examples>
//  int $array1[] = { 1, 2, 3 };
//  // Result: 1 2 3 //
//  intArrayRemoveAtIndex(1, $array1);
//  // Result: 1 //
//  print $array1;
//  // Result: 1 3 //
//
//</doc>
//

global proc int intArrayRemoveAtIndex( int $index, int $list[] )
{
	int $i;
	int $result[];
	int $len = size( $list );

	// Check for a valid index.
	//
	if ( $index >= $len || $index < 0 ) {
		return( false );
	}

	// Copy any entries prior to $index.
	//
	for ( $i = 0; $i < $index; $i++ ) {
		$result[$i] = $list[$i];
	}

	// Copy any items which come after $index.
	//
	for ( ; $i < $len - 1; $i++ ) {
		$result[$i] = $list[$i + 1];
	}

	// Copy the result to the list argument.
	//
	$list = $result;

	return( true );
}
