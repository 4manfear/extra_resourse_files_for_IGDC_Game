// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================

global proc setRenderAllLayersMode(string $parent)
{
	setParent $parent;

	if (`radioButtonGrp -q -select renderViewRenderAllLayersModeRadio`)
		optionVar -intValue renderViewRenderLayersMode 0;
	else if (`radioButtonGrp -q -select renderViewRenderAllLayersModeRadio2`)
		optionVar -intValue renderViewRenderLayersMode 1;
	else 
		optionVar -intValue renderViewRenderLayersMode 2;
}

global proc resetRenderAllLayersMode(string $parent)
{
	setParent $parent;
	optionVar -intValue renderViewRenderLayersMode 1;
	radioButtonGrp -e -select 1 renderViewRenderAllLayersModeRadio;
}

global proc switchRenderAllLayersOptBox ()
{
	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate DefaultTemplate;
	waitCursor -state 1;
        
	string $parent = `columnLayout -adjustableColumn 1`;

    radioButtonGrp -nrb 1 -label (uiRes("m_switchRenderAllLayersOptBox.kKeepImageMode")) 
		-label1 (uiRes("m_switchRenderAllLayersOptBox.kCompositeLayers"))
		renderViewRenderAllLayersModeRadio;

	radioButtonGrp -nrb 1
		-label1 (uiRes("m_switchRenderAllLayersOptBox.kCompositeKeepLayers"))
		-shareCollection renderViewRenderAllLayersModeRadio
		renderViewRenderAllLayersModeRadio2;

	radioButtonGrp -nrb 1
		-label1 (uiRes("m_switchRenderAllLayersOptBox.kKeepLayers"))
		-shareCollection renderViewRenderAllLayersModeRadio
		renderViewRenderAllLayersModeRadio3;

	int $mode = `optionVar -q renderViewRenderLayersMode`;
	switch ($mode)
	{
		case 0:
		radioButtonGrp -e -select 1 renderViewRenderAllLayersModeRadio;
		break;

		case 1:
		radioButtonGrp -e -select 1 renderViewRenderAllLayersModeRadio2;
		break;

		default:
		radioButtonGrp -e -select 1 renderViewRenderAllLayersModeRadio3;
		break;
	}

	waitCursor -state 0;
	setUITemplate -popTemplate;

	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -command ("setRenderAllLayersMode " + $parent)
		$applyBtn;
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit -command ("setRenderAllLayersMode " + $parent)
		$saveBtn;
	string $resetBtn = getOptionBoxResetBtn();
	button -edit -command ("resetRenderAllLayersMode " + $parent)
		$resetBtn;
                         
	setOptionBoxTitle (uiRes("m_switchRenderAllLayersOptBox.kRenderAllLayersOptions"));

	setOptionBoxHelpTag( "RenderAllLayersOptions" );

	showOptionBox();
}
