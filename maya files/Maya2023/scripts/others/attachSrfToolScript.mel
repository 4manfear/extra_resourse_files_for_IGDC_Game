// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  5 January 1999
//
//
//  Procedure Name:
//      alignCurvesToolScript
//
//  Description:
//	create wrapper around the alignCurve command
//
//  Input Arguments:
//      what is going to happen when this script is called
//		$setToTool = 0 ==> do the command
//		$setToTool = 1 ==> show option box
//		$setToTool = 2 ==> return the command
//		$setToTool = 3 ==> show tool option box
//		$setToTool = 4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc attachSrfToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;

	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Attach"
		-iv attachSurfaceEuc 0
		-iv attachSurfaceLac $gStartWithAutoComplete
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q attachSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q attachSurfaceLac` $goToTool;
	}
}

global proc attachSurfaceValues ( string $toolName ) 
{
	performAttachSrf 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc attachSurfaceProperties()
{
	scriptCtxCommonProperties;
}

proc createAttachSrfContext ( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		attachSrfToolSetup( 0, "" );
		scriptCtx
			-i1 "align.png"
			-bcn "attachSurface"

			-title (uiRes("m_attachSrfToolScript.kAttachSurfacesTool"))
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("attachSrfToolScript 0")

			-setNoSelectionPrompt ((uiRes("m_attachSrfToolScript.kSelectTwoSurfaces")))
			-setSelectionPrompt ((uiRes("m_attachSrfToolScript.kSelectSecondSurface")))
			-setDoneSelectionPrompt ((uiRes("m_attachSrfToolScript.kPressEnterToComplete")))

			-setAutoToggleSelection true
			-showManipulators true
			-nurbsSurface true
			-isoparm true
   			-surfaceEdge true
   			-polymeshEdge true

			-setAutoComplete `optionVar -q attachSurfaceLac`
			-setSelectionCount 2

			// If this was true, the tool would exit
			// once the attach has been completed
			-exitUponCompletion `optionVar -q attachSurfaceEuc`

		$tool;
	}
}

global proc string attachSrfToolScript( int $setToTool )
//
//	Description :
//		$setToTool = 0 ==> do the command
//		$setToTool = 1 ==> show option box
//		$setToTool = 2 ==> return the command
//		$setToTool = 3 ==> show tool/action option box
//		$setToTool = 4 ==> enter the tool/do the action
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolAttachSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "attachSrfContext";
			createAttachSrfContext $tool;
		}
	}
	return scriptToolScript( "performAttachSrf", $setToTool, $tool );
}
