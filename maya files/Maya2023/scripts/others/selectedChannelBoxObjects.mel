// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//	Procedure Name:
//		selectedChannelBoxObjects()
//
//	Description:
//		Returns the list of all objects that have currently
//		selected attributes in the channel box.  If no
//		attributes are selected, the returned string[] is empty.
//
//  Input Arguments:
//		None.
//
//  Return Value:
//      String array of object names
//

global proc string[] selectedChannelBoxObjects() 
{
	global 	string $gChannelBoxName; 
	string  $result[];
	
	if( `channelBox -q -exists $gChannelBoxName` ) {
		string 	$main[], $mainObjects[];
		string 	$shape[], $shapeObjects[];
		string 	$history[], $historyObjects[];
		string  $outputs[], $outputObjects[];
		
		
		$main = `channelBox -q -selectedMainAttributes $gChannelBoxName`;
		$mainObjects = `channelBox -q -mainObjectList $gChannelBoxName`;

		$shape = `channelBox -q -selectedShapeAttributes $gChannelBoxName`;
		$shapeObjects = `channelBox -q -shapeObjectList $gChannelBoxName`;

		$history = `channelBox -q -selectedHistoryAttributes $gChannelBoxName`;
		$historyObjects = `channelBox -q -historyObjectList $gChannelBoxName`;

		$outputs = `channelBox -q -selectedOutputAttributes $gChannelBoxName`;
		$outputObjects = `channelBox -q -outputObjectList $gChannelBoxName`;
		
		if( size( $main ) > 0 ) {
			for( $object in $mainObjects ) {
				$result[ size($result) ] = $object;			
			}
		}

		if( size( $shape ) > 0 ) {
			for( $object in $shapeObjects ) {
				$result[ size($result) ] = $object;			
			}
		}

		if( size( $history ) > 0 ) {
			for( $object in $historyObjects ) {
				$result[ size($result) ] = $object;			
			}
		}

		if( size( $outputs ) > 0 ) {
			for( $object in $outputObjects ) {
				$result[ size($result) ] = $object;			
			}
		}
	}

	return $result;
}
