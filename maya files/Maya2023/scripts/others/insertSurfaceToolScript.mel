// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      insertSurfaceToolScript
//
//  Description:
//	create wrapper around the insertSurface command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc insertSurfaceToolSetup( int $forceFactorySettings,
									string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Insert Isoparms"
		-iv insertSurfaceEuc 0
		-iv insertSurfaceLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q insertSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q insertSurfaceLac` $goToTool;
	}
}

global proc insertSurfaceProperties()
{
	scriptCtxCommonProperties;
}

global proc insertSurfaceValues ( string $toolName ) 
{
	performInsertKnot 3 "Surface" $toolName;
	scriptCtxCommonValues( $toolName );
}
proc createInsertSurfaceContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		insertSurfaceToolSetup( 0, "" );
		scriptCtx -i1 "insert.png"
		  -title (uiRes("m_insertSurfaceToolScript.kInsertIsoparmTool"))
		  -bcn "insertSurface"

		  -exitUponCompletion `optionVar -q insertSurfaceEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("insertSurfaceToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_insertSurfaceToolScript.kSelectIsoparm")) )
		  -setSelectionPrompt ((uiRes("m_insertSurfaceToolScript.kSelectAnotherIsoparm")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q insertSurfaceLac`
		  -showManipulators true
		  -setSelectionCount 0
		  -isoparm true
		$tool;
	}
}

global proc string performInsertKnotSrf( int $action,
										 string $goToTool )
{
	return performInsertKnot( $action, "Surface", $goToTool );
}

global proc string insertSurfaceToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolInsertSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "insertSurfaceContext";
			createInsertSurfaceContext $tool;
		}
	}
	return scriptToolScript( "performInsertKnotSrf", $setToTool, $tool );
}
