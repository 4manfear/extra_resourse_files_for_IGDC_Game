// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//
//
//
//
//  Creation Date:  26 Jun 1996
//
//  Description:
//      This procedure sets the state of the object pick mask.
//
//  Input Arguments:
//		The flag for the pick mask setting to turn on/off,
//		and the state to set that pick mask setting to.
//
//  Return Value:
//		None
//

global proc setObjectPickMask( string $maskType, int $state ) {

	//
	// Set values, based on the state of the pick mask
	// type and state passed to the procedure
	//
	global string $gSelMarkerFilterTypeList[];
	global string $gSelJointFilterTypeList[];
	global string $gSelCurveFilterTypeList[];
	global string $gSelSurfaceFilterTypeList[];
	global string $gSelDeformerFilterTypeList[];
	global string $gSelDynamicFilterTypeList[];
	global string $gSelRenderingFilterTypeList[];
	global string $gSelOtherFilterTypeList[];

	string $selTypeName;
	switch ($maskType) {
		case "All":
			selectType -allObjects $state;
			//
			// Change the state of all the object
			// selection mask buttons in the infoBar
			//
			iconTextCheckBox -e -v $state objSelHandleBtn;
			iconTextCheckBox -e -v $state objSelJointBtn;
			iconTextCheckBox -e -v $state objSelCurveBtn;
			iconTextCheckBox -e -v $state objSelSurfaceBtn;
			iconTextCheckBox -e -v $state objSelDeformBtn;
			iconTextCheckBox -e -v $state objSelDynamicBtn;
			iconTextCheckBox -e -v $state objSelRenderBtn;
			break;
		case "Marker":
			for ($selTypeName in $gSelMarkerFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-handle $state
				-ikHandle $state;
			break;
		case "Joint":
			for ($selTypeName in $gSelJointFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-joint $state;
			break;
		case "Curve":
			for ($selTypeName in $gSelCurveFilterTypeList)
				eval selectType -byName $selTypeName $state;
			if (`isTrue MayaCreatorExists`) {
				selectType
					-nurbsCurve $state
					-cos $state
					-stroke $state;  // For Particle Paint
			}
			else 
			{
				selectType
					-nurbsCurve $state
					-cos $state;
			}
			break;
		case "Surface":
			for ($selTypeName in $gSelSurfaceFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-nurbsSurface $state
				-polymesh $state
				-subdiv $state
				-plane $state;
			break;
		case "Deformer":
			for ($selTypeName in $gSelDeformerFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-lattice $state
				-cluster $state
				-sculpt $state
				-nonlinear $state;			
			break;
		case "Dynamic":
			for ($selTypeName in $gSelDynamicFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-particleShape $state
				-emitter $state
				-field $state
				-fluid $state
				-hairSystem $state
				-follicle $state
				-nCloth $state
				-nRigid $state
				-dynamicConstraint $state
				-spring $state
				-collisionModel $state
				-rigidBody $state
				-rigidConstraint $state;
			break;
		case "Rendering":
			for ($selTypeName in $gSelRenderingFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-light $state
				-camera $state
				-texture $state;
			break;
		case "Other":
			for ($selTypeName in $gSelOtherFilterTypeList)
				eval selectType -byName $selTypeName $state;
			selectType
				-ikEndEffector $state
				-locator $state
				-dimension $state;
			break;
	}
}
