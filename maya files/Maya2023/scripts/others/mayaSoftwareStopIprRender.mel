// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
// Description:  This procedure is called to stop the maya software 
//      renderer IPR session.
//
global proc mayaSoftwareStopIprRender()
{
    // Stop IPR if it is still running.
    //
	if (isIprFileLoaded()) {
	    // Delete the IPR engine
	    //
	    if (isIprFileLoaded()) {
	        iprEngine
	            -edit
	            -releaseIprImage
	            defaultIprEngine;
	
	        deleteUI defaultIprEngine;
	    }
	
	    // Delete the IPR default light, if there is one.  If there is indeed one,
	    // it will be the directional light with the dynamic attribute
	    // isIPRDefaultLight.
	    //
	
	    string $directionalLights[] = `ls -type directionalLight`;
	
	    string $light;
	
	    for ($light in $directionalLights)
	    {
	        if (`objExists ($light + ".isIPRDefaultLight")`)
	        {
	            // Delete the transform (i.e., parents[0]), not just the light,
	            // and make sure it doesn't go on undo queue.
	            //
	            string $parents[] = `listRelatives -parent $light`;
	            int $undoOn = `undoInfo -query -swf`;
	            if ($undoOn)
	                undoInfo -swf off;
	
	            catch(`delete $parents[0]`);
	            if ($undoOn)
	                undoInfo -swf on;
	
	            break;
	        }
	    }
	
	    // Update the memory estimate.
	    //
	    updateIPRMemoryEstimate();
	}
}
