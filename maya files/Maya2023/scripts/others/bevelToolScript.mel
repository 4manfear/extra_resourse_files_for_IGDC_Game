// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      bevelToolScript
//
//  Description:
//	create wrapper around the bevel command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc bevelToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;

	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Bevel"
		-iv bevelEuc 0
		-iv bevelLac $gStartWithAutoComplete
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q bevelEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q bevelLac` $goToTool;
	}
}

global proc bevelValues ( string $toolName ) 
{
	performBevel 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc bevelProperties()
{
	scriptCtxCommonProperties;
}

proc createBevelContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		bevelToolSetup( 0, "" );
		scriptCtx -i1 "bevel.png"
		  -title (uiRes("m_bevelToolScript.kBevelTool"))
		  -bcn "bevel"

		  -exitUponCompletion `optionVar -q bevelEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("bevelToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_bevelToolScript.kSelect"))
		  -setSelectionPrompt (uiRes("m_bevelToolScript.kComputeBevel"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q bevelLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		$tool;
	}
}

global proc string bevelToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolBevel` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "bevelContext";
			createBevelContext $tool;
		}
	}
	return scriptToolScript( "performBevel", $setToTool, $tool );
}
