// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//  Creation Date:  December 2012
//

//
//  Procedure Name:
//		doHideObjects
//
//  Description:
//		Hide the selected or unselected object(s).
//
//  Input Arguments:
//		int $selected - If true hide the selected objects.
//						If false hide the unselected objects.
//
global proc doHideObjects( int $selected )
{
	string $objs[];
	string $nonMayaObjs[];
	string $msgIvm;
	int $invertComponents = 0;

	//	Are we hiding the selected or unselected objects?
	if ( $selected )
	{
		$objs = `ls -sl`;
		if ( `selectMode -q -object` )
		{
			$nonMayaObjs = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.getNonMayaSelectedItems()")`;
		}
		if ((size($objs) != 0) || (size($nonMayaObjs) != 0)) {
			if ( `selectMode -q -co` )
				$msgIvm = (uiRes("m_doHideObjects.kIvmHideSelectedComponents"));
			else
				$msgIvm = (uiRes("m_doHideObjects.kIvmHideSelected"));
		} else {
			$msgIvm = (uiRes("m_doHideObjects.kIvmInvalidSelected"));
		}
	}
	else
	{
		if ( `selectMode -q -co` )
		{
			$invertComponents = 1;
			$objs = `ls -sl`;
			if (size($objs) != 0) {
				$msgIvm = (uiRes("m_doHideObjects.kIvmHideUnselectedComponents"));
			} else {
				$msgIvm = (uiRes("m_doHideObjects.kIvmInvalidUnSelectedComponents"));
			}
		}
		else
		{
			if ( `selectMode -q -object` )
			{
				$haveNonMaya = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.hasNonMayaSelectedItems()")`;
				if ($haveNonMaya)
				{
					$objs = `python("import maya.internal.ufeSupport.utils as ufeUtils; ufeUtils.listUFEUnselected()")`;
				}
				else
				{
					$objs = `listUnselected`;
				}
			}
			else
			{
				$objs = `listUnselected`;
			}
			if (size($objs) != 0) {
				$msgIvm = (uiRes("m_doHideObjects.kIvmHideUnselected"));
			} else {
				$msgIvm = (uiRes("m_doHideObjects.kIvmInvalidUnSelected"));
			}

		}
	}

	//	Perform the actual hide command (remembering the list of objects we hid)
	//	and then display an in-view message.
	string $objsToHide[] = stringArrayCatenate($objs, $nonMayaObjs);
	hideKeepSelection( $objsToHide, $invertComponents );
	inViewMessage -amg $msgIvm -fade;
}
