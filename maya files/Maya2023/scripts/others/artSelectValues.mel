// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  June 2000
//
//  Description:
//      Option tool property window for Artisan tools
//
//
//  Procedure Name:
//      artSelectValues
//
//  Description:
//      Initialize the option values.
//
//  Input Arguments:
//
//  Return Value:
//      None.
//

proc artSelectOperValues(
	string	$artCommand,
	string	$currTool
)
//
//	Description:
//		 Paint Select operation values.
//
{
	// Paint Select operation.
	string $cmd = ($artCommand + " -q -selectop " + `currentCtx` );
	string $oper = eval($cmd);
	if ( $oper == "select" ) {
		radioButtonGrp -e -sl 1 artSelectOperRadioButton0;
	} else if ( $oper == "unselect" ) {
		radioButtonGrp -e -sl 2 artSelectOperRadioButton0;
	} else if ( $oper == "toggle" ) {
		radioButtonGrp -e -sl 1 artSelectOperRadioButton1;
	}

	// Add to selection flag.
	$cmd = ($artCommand + " -q -addselection " + `currentCtx` );	
	checkBoxGrp -e -v1 `eval $cmd` artSelectAddToCurrentChk;
}

// =======================================================
// Main Procedure.
// =======================================================
global proc artSelectValues(
	string 	$toolName
)
{
	source "artisanCallback.mel";
	source "artisanValues.mel";

	string 	  $parent = (`toolPropertyWindow -q -location` + "|artSelect");
	setParent $parent;

	string $icon = "artSelect.png";
	string $helpTag = "PaintSelectionTool";
	toolPropertySetCommon $toolName $icon $helpTag;

	string $artCommand = "artSelectCtx";
	
	string $currContext = `currentCtx`;
	string $currTool    = `contextInfo -c $currContext`;

	// Brush frameLayout values.
    artisanBrushValues( $artCommand, $currTool );

	// Paint Select operation frameLayout values.
	artSelectOperValues( $artCommand, $currTool );

	// Stroke frameLayout values.
	artisanStrokeValues( $artCommand, $currTool );

	// Stylus Pressure frameLayout values.
	artisanPressureValues( $artCommand, $currTool );

	// Attribute Maps frameLayout values.
	artisanAttrMapValues( $artCommand, $currTool );

	// Display frameLayout values.
	artisanDisplayValues( $artCommand, $currTool );

	toolPropertySelect artSelect;
}
