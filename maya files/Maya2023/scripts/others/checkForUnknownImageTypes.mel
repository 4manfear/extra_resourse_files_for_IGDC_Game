// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  17.Mar.2004
//
//  Procedure Name:
//      checkForUnknownImageTypes
//
//  Description:
//		Check the scene for unknown output image formats. 
//		If unknown image format is found, convert it to Maya iff.
//		Image format becomes unknown to a platform 
//		due to IMF/Quicktime differences or unavailable imf plugins.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//

proc int checkForValidImfKey(int $imageformat, string $imfPluginKey )
{
	global string $gImfKey[];  // This is the unique imf keyword

	if (size($gImfKey) == 0) {
		// If the file format array has not been initialized yet, do so.
		//
		createImageFormats();
	}

	int $unknownFormat = 1;
	if ($imageformat == 50) {
		// new imf plugin
		//
		global string $gImfPlugInKey[];
		int $i;
		for ($i=0; $i<size($gImfPlugInKey); $i++) {
			if ($imfPluginKey == $gImfPlugInKey[$i]) {
				$unknownFormat = 0;
				break;
			}
		}
	}
	else {
		// Make sure the the format is availave on this platform. 
		// Flash formats, above 60 have no $gImfKey set, but are
		// valid.
		if (($gImfKey[$imageformat] != "") || 
			($imageformat == 51) || ($imageformat >= 60))
			$unknownFormat = 0;
	}
	
	return $unknownFormat;
}

global proc checkForUnknownImageTypes()
{
	// check whether software render globals image format is unknown
	//
	int $imageformat  = `getAttr defaultRenderGlobals.imageFormat`;
	string $imfPluginKey = `getAttr defaultRenderGlobals.imfkey`;

	int $unknownFormat = checkForValidImfKey($imageformat, $imfPluginKey);
	if ($unknownFormat) {
		warning (uiRes("m_checkForUnknownImageTypes.kUnknownImageFormat"));

		setAttr defaultRenderGlobals.imageFormat 32;
		setAttr defaultRenderGlobals.imfkey -type "string" "";
	}
}
