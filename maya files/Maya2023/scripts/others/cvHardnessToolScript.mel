// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      cvHardnessToolScript
//
//  Description:
//	create wrapper around the cvHardness command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc cvHardnessToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Curves.CV Hardness"
		-iv cvHardnessEuc 0
		-iv cvHardnessLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q cvHardnessEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q cvHardnessLac` $goToTool;
	}
}

global proc cvHardnessValues ( string $toolName ) 
{
	performhardenPointCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc cvHardnessProperties()
{
	scriptCtxCommonProperties;
}

proc createCvHardnessContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		cvHardnessToolSetup( 0, "" );
		scriptCtx -i1 "cvHardness.png"
			-title (uiRes("m_cvHardnessToolScript.kCVHardnessCurvesTool"))
			-bcn "cvHardness"

			-exitUponCompletion `optionVar -q cvHardnessEuc`
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("cvHardnessToolScript 0")

			-setNoSelectionPrompt (uiRes("m_cvHardnessToolScript.kSelectCurveCVs"))
			-setSelectionPrompt (uiRes("m_cvHardnessToolScript.kComplete"))

			-setAutoToggleSelection true
			-setAutoComplete `optionVar -q cvHardnessLac`
			-showManipulators true
			-setAllowExcessCount true
			-setSelectionCount 0

			-cv true
		$tool;
	}
}

global proc string cvHardnessToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolCvHardness` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "cvHardnessContext";
			createCvHardnessContext $tool;
		}
	}
	return scriptToolScript( "performhardenPointCurve", $setToTool, $tool );
}
