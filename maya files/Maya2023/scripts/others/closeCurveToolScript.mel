// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      closeCurveToolScript
//
//  Description:
//	create wrapper around the closeCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc closeCurveToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Curves.Open/Close"
		-iv closeCurveEuc 0
		-iv closeCurveLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q closeCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q closeCurveLac` $goToTool;
	}
}

global proc closeCurveValues ( string $toolName ) 
{
	performCloseCurve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc closeCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createCloseCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		closeCurveToolSetup( 0, "" );
		scriptCtx -i1 "closeGeom.png"
		  -title (uiRes("m_closeCurveToolScript.kOpenCloseCurveTool"))
		  -bcn "closeCurve"

		  -exitUponCompletion `optionVar -q closeCurveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("closeCurveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_closeCurveToolScript.kSelectCurve"))
		  -setSelectionPrompt (uiRes("m_closeCurveToolScript.kSelectAdditionalCurve"))
		  -setDoneSelectionPrompt (uiRes("m_closeCurveToolScript.kOpenCloseCurve"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q closeCurveLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsCurve true
		  -curveOnSurface true
		$tool;
	}
}

global proc string closeCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolCloseCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "closeCurveContext";
			createCloseCurveContext $tool;
		}
	}
	return scriptToolScript( "performCloseCurve", $setToTool, $tool );
}
