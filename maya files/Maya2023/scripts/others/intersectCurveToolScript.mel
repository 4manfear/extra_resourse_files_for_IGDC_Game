// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      intersectCurveToolScript
//
//  Description:
//	create wrapper around the intersectCurve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc intersectCurveToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Curves.Intersect"
		-iv intersectCurveEuc 1
		-iv intersectCurveLac 0
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q intersectCurveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q intersectCurveLac` $goToTool;
	}
}

global proc intersectCurveValues ( string $toolName ) 
{
	performIntersectCrv 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc intersectCurveProperties()
{
	scriptCtxCommonProperties;
}

proc createIntersectCurveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		intersectCurveToolSetup( 0, "" );
		int $n = 0;
		if( `optionVar -q intersectCurveLac` ) {
			$n = 2;
		}
		scriptCtx -i1 "intersectCurves.png"
			-title (uiRes("m_intersectCurveToolScript.kIntersectCurvesTool"))
			-bcn "intersectCurve"

			-exitUponCompletion `optionVar -q intersectCurveEuc`
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("intersectCurveToolScript 0")

			-setNoSelectionPrompt (uiRes("m_intersectCurveToolScript.kSelectTwoCurves"))
			-setSelectionPrompt (uiRes("m_intersectCurveToolScript.kSelectSecondCurve"))

			-setAutoToggleSelection true
			-setAutoComplete `optionVar -q intersectCurveLac`
			-showManipulators true
			-setSelectionCount $n

			-nurbsCurve true
		$tool;
	}
}

global proc string intersectCurveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolIntersectCurve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "intersectCurveContext";
			createIntersectCurveContext $tool;
		}
	}
	return scriptToolScript( "performIntersectCrv", $setToTool, $tool );
}
