// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  2003
//
//  Description:
//	     Option box for straighten menu.	
//


//  Procedure Name:
//      setOptionVars
//
//  Description:
//		Initialize the option values.
//
//  Input Arguments:
//		Whether to set the options to default values.
//
//
proc setOptionVars(int $forceFactorySettings)
{
	optionVar -init $forceFactorySettings -category "Dynamics.nHair.Modify Curves.Straighten"
		-fv straightenStraightness 1.0
		-iv straightenPreserveLength 1
		;
}

//
//  Procedure Name:
//      performStraightenSetup
//
//  Description:
//		Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//		forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc performStraightenSetup(string $parent, int $forceFactorySettings)
{
	//	Retrieve the option settings
	//
	setOptionVars($forceFactorySettings);

	setParent $parent;

	floatSliderGrp -edit 
		-v `optionVar -query straightenStraightness`
		straightenStraightness;

	checkBoxGrp -edit 
		-value1 `optionVar -query straightenPreserveLength`
		straightenPreserveLength;

}

//
//  Procedure Name:
//      performStraightenCallback
//
//  Description:
//		Update the option values with the current state of the option box UI.
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//		doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc performStraightenCallback(string $parent, int $doIt)
{
    setParent $parent;

	optionVar -floatValue straightenStraightness
		`floatSliderGrp -query -v straightenStraightness`;

	optionVar -intValue straightenPreserveLength
		`checkBoxGrp -query -value1 straightenPreserveLength`;

	if( $doIt ) {
		performStraighten 0; 
	}
}


//
//  Procedure Name:
//      straightenOptions
//
//  Description:
//		Construct the option box UI.  Involves accessing the standard option
//		box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc straightenOptions()
{
	//	Name of the command for this option box.
	//
	string $commandName = "performStraighten";

	//	Build the option box actions.
	//
	string $callback = ($commandName + "Callback");
	string $setup = ($commandName + "Setup");

	string $layout = getOptionBox();
	setParent $layout;
	
	setUITemplate -pushTemplate DefaultTemplate;

	tabLayout -tv false -scr true;
	
	string $parent = `columnLayout -adjustableColumn 1`;

    floatSliderGrp
		-label (uiRes("m_performStraighten.kStraightness"))
		-field 1
		-min 0 
		-max 2.0
		-fieldMinValue -100000.0
		-fieldMaxValue 100000.0
		-pre 3
		straightenStraightness;

    checkBoxGrp -ncb 1 -label1 (uiRes("m_performStraighten.kPreserveLength"))
		straightenPreserveLength;


	//	'Apply' button.
	//
	string $applyBtn = getOptionBoxApplyBtn();
	button -edit
		-label (uiRes("m_performStraighten.kStraightenCurves"))
		-command ($callback + " " + $parent + " " + 1)
		$applyBtn;

	//	'Save' button.
	//
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		-command ($callback + " " + $parent + " " + 0 + "; hideOptionBox")
		$saveBtn;

	//	'Reset' button.
	//
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		-command ($setup + " " + $parent + " " + 1)
		$resetBtn;

	//	Set the option box title.
	//
	setOptionBoxTitle (uiRes("m_performStraighten.kStraightenCurvesOptions"));
	setOptionBoxCommandName($commandName);
	setOptionBoxHelpTag( "Straighten" );

	// Pop the UI template
	//
	setUITemplate -popTemplate;

	//	Set the current values of the option box.
	//
	eval( $setup + " " + $parent + " " + 0 );	
	
	//	Show the option box.
	//
	showOptionBox();
}

//
//  Procedure Name:
//      assembleCmd
//
//  Description:
//		Construct the command that will apply the option box values.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
proc string assembleCmd()
{
	setOptionVars(false);

	string $cmd = ("modifySelectedCurves straighten " 
					+ `optionVar -query straightenStraightness` +  " "
					+ `optionVar -query straightenPreserveLength` );

	return $cmd;
}

//
//  Procedure Name:
//      performStraighten
//
//  Description:
//		Perform the command using the corresponding 
//		option values.  This procedure will also show the option box
//		window if necessary as well as construct the command string
//		that will invoke the command with the current 
//		option box values.
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
//  Return Value:
//      None.
//
global proc string performStraighten(int $action)
{
	string $cmd = "";

	switch ($action) {

		//	Execute the command from option settings.
		//
		case 0:
			setOptionVars(false);
			$cmd = `assembleCmd`;
			eval($cmd);
			break;

		//	Show the option box.
		//
		case 1:
			straightenOptions();
			break;

		//	Return the command string.
		//
		case 2:
			setOptionVars (false);
			$cmd = `assembleCmd`;
			break;
	}
	return $cmd;
}

