// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      revolveToolScript
//
//  Description:
//	create wrapper around the revolve command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc revolveToolSetup( int $forceFactorySettings, string $goToTool )
{
	global int $gStartWithAutoComplete;

	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Revolve"
		-iv revolveEuc 0
		-iv revolveLac $gStartWithAutoComplete
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q revolveEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q revolveLac` $goToTool;
	}
}

global proc revolveValues ( string $toolName ) 
{
	performRevolve 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc revolveProperties()
{
	scriptCtxCommonProperties;
}

proc createRevolveContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		revolveToolSetup( 0, "" );
		scriptCtx -i1 "revolve.png"
		  -title (uiRes("m_revolveToolScript.kRevolveTool"))
		  -bcn "revolve"

		  -exitUponCompletion `optionVar -q revolveEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("revolveToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_revolveToolScript.kSelectNURBSCurves"))
		  -setSelectionPrompt (uiRes("m_revolveToolScript.kSelectAdditionalNURBSCurves"))
		  -setDoneSelectionPrompt (uiRes("m_revolveToolScript.kPressEnter"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q revolveLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		$tool;
	}
}

global proc string revolveToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolRevolve` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "revolveContext";
			createRevolveContext $tool;
		}
	}
	return scriptToolScript( "performRevolve", $setToTool, $tool );
}
