// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      projectTangentToolScript
//
//  Description:
//	create wrapper around the projectTangent command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc projectTangentToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Curves.Project Tangent"
		-iv projectTangentEuc 0
		-iv projectTangentLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q projectTangentEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q projectTangentLac` $goToTool;
	}
}

global proc projectTangentValues ( string $toolName ) 
{
	performProjectTangent 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc projectTangentProperties()
{
	scriptCtxCommonProperties;
}

proc createProjectTangentContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		projectTangentToolSetup( 0, "" );
		scriptCtx -i1 "projectTangent.png"
			-title (uiRes("m_projectTangentToolScript.kProjectCurvesTool"))
			-bcn "projectTangent"

			-exitUponCompletion `optionVar -q projectTangentEuc`
			-totalSelectionSets 1
			-expandSelectionList true
			-fcs ("projectTangentToolScript 0")

			-setNoSelectionPrompt (uiRes("m_projectTangentToolScript.kSelect"))
			-setSelectionPrompt (uiRes("m_projectTangentToolScript.kComplete"))

			-setAutoToggleSelection true
			-setAutoComplete `optionVar -q projectTangentLac`
			-showManipulators true
			-setSelectionCount 0

			-nurbsCurve true
			-nurbsSurface true
			-isoparm true
		$tool;
	}
}

global proc string projectTangentToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolProjectTangent` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "projectTangentContext";
			createProjectTangentContext $tool;
		}
	}
	return scriptToolScript( "performProjectTangent", $setToTool, $tool );
}
