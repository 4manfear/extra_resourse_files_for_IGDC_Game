// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//  Creation Date:  12/01/2016
//
//  Description:
//      Performs unstack UV shells
//

//
//  Procedure Name:
//      unstackShellsOptions
//
//  Description:
//      Construct the option box UI.  Involves accessing the standard option
//      box and customizing the UI accordingly.
//
//  Input Arguments:
//      None.
//
//  Return Value:
//      None.
//
global proc unstackShellsOptions()
{

	//  Name of the command for this option box.
    //
    string $commandName;
   
    $commandName = "UnstackShells";
	
    //  Build the option box actions.
    //
    string $callback = $commandName + "Callback";
    string $setup    = $commandName + "Setup";
    
    //  STEP 1:  Get the option box.
    //  ============================
    //
    //  The value returned is the name of the layout to be used as
    //  the parent for the option box UI.
    //
    string $layout = getOptionBox();
	
    //  STEP 2:  Pass the command name to the option box.
    //  =================================================
    //
    //  Any default option box behaviour based on the command name is set
    //  up with this call. For example, updating the 'Help' menu item with
    //  the name of the command.
    //  TODO: Check with Doc team
    setOptionBoxCommandName($commandName);
    
    //  STEP 3:  Create option box contents.
    //  ====================================
    //
	setParent $layout;
	    $parent = unstackShellsOptionsUI();
	setParent ..;
    
    //  STEP 4:  Customize the buttons.
    //  ===============================
    //
    //  Provide more descriptive labels for the buttons.
    //  Disable those buttons that are not applicable to the option box.
    //  Attach actions to those buttons that are applicable to the option box.
  
    string $applyAndCloseBtn = getOptionBoxApplyAndCloseBtn();
    button -e -command ($callback + " " + $parent + " 1; hideOptionBox;") $applyAndCloseBtn;
	
    string $applyBtn = getOptionBoxApplyBtn();
    button -e -command ($callback + " " + $parent + " 1;") $applyBtn;

    string $resetBtn = getOptionBoxResetBtn();
    button -e -command($setup + " " + $parent + " 1") $resetBtn;

    string $saveBtn = getOptionBoxSaveBtn();
    button -e -command($callback + " " + $parent + " 0; hideOptionBox;") $saveBtn;
		   
    //  STEP 5:  Set the option box title.
    //  ==================================
    //
    setOptionBoxTitle((uiRes("m_performUnstackShells.kUnstackShellsOptions")));

    // STEP 6:  Customize the 'Help' menu item text.
    // TODO: Check with doc team
    //  =============================================
    //
    setOptionBoxHelpTag($commandName);

    //  Set the current values of the option box.
    //  =========================================
    //
    eval ($setup + " " + $parent + " 0");

    //  Show the option box.
    //  ====================
    //
    showOptionBox();
}

//
//  Procedure Name:
//      unstackShellsOptionsUI
//
//  Description:
//      Build options UI
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//  Return Value:
//      Layout.
//
global proc string unstackShellsOptionsUI()
{
	$layout = `columnLayout -adjustableColumn true`;
		//Settings
		frameLayout -label (uiRes("m_performUnstackShells.kSettings"));
            checkBoxGrp -vr -ncb 1 -label "" 
			    -label1 (uiRes("m_performUnstackShells.kFlipReversedUVShells"))
				texUnstackShellsSettingsCheckBox;
		setParent ..;
	
	setParent..;
	return $layout;
}

//
//  Procedure Name:
//      unstackShellsCallback
//
//  Description:
//      Update the option values and execute command
//
//  Input Arguments:
//      parent - Top level parent layout of the option box UI.  Required so
//               that UI object names can be successfully resolved.
//
//  	doIt   - Whether the command should execute.
//
//  Return Value:
//      None.
//
global proc UnstackShellsCallback(string $parent, int $doIt)
{
    unstackShellsSetOptionVars (false);
	setParent $parent;
	if(`checkBoxGrp -q -ex texUnstackShellsSettingsCheckBox`)
		optionVar -iv unstackShellsFlipReversedUV (`checkBoxGrp -q -value1 texUnstackShellsSettingsCheckBox`);
    if($doIt)
        performUnstackShells 0;
}

//
//  Procedure Name:
//      unstackShellsSetup
//
//  Description:
//      Update the state of the option box UI to reflect the option values.
//
//  Input Arguments:
//      parent               - Top level parent layout of the option box UI.
//                             Required so that UI object names can be 
//                             successfully resolved.
//
//      forceFactorySettings - Whether the option values should be set to
//                             default values.
//
//  Return Value:
//      None.
//
global proc UnstackShellsSetup(string $parent, int $forceFactorySettings)
{
    // Retrieve the option settings
    //
    unstackShellsSetOptionVars ($forceFactorySettings);
    setParent $parent;
	if(`checkBoxGrp -q -ex texUnstackShellsSettingsCheckBox`)
		checkBoxGrp -e -value1 (`optionVar -q unstackShellsFlipReversedUV`) texUnstackShellsSettingsCheckBox;
}

//
//  Procedure Name:
//      unstackShellsSetOptionVars
//
//  Description:
//      Initialize the option values.
//
//  Input Arguments:
//      forceFactorySettings - Whether to set the options to default values.
//
//  Return Value:
//      None.
//
global proc unstackShellsSetOptionVars(int $forceFactorySettings)
{
	optionVar -init $forceFactorySettings -category "UV Editor.Unstack Shells"
		-iv unstackShellsFlipReversedUV 1
		;
}

proc string assembleCmd()
{
	unstackShellsSetOptionVars(false);
    string $cmd = "texUnstackShells ";
	$cmd += `optionVar -q unstackShellsFlipReversedUV`;
    return $cmd;
}

//
//  Procedure Name:
//      performUnstackShells
//
//  Description:
//      Prepare parameters and run texunstackShells
//
//  Input Arguments:
//      0 - Execute the command.
//      1 - Show the option box dialog.
//      2 - Return the command.
//
global proc string performUnstackShells(int $action)
{
	string $cmd = "";
	
	switch ($action) {

		//	Execute the command.
		//
		case 0:
			$cmd = assembleCmd();
            //  Execute the command with the option settings.
            //
            evalEcho($cmd);
			break;

		//	Show the option box.
		//
		case 1:
			unstackShellsOptions();
			break;

		//	Return the command string.
		//
		case 2:
			//	Get the command.
			//
			$cmd = assembleCmd();
			break;
	}
	return $cmd;
}