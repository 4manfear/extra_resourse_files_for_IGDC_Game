// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      reverseSurfaceToolScript
//
//  Description:
//	create wrapper around the reverseSurface command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc reverseSurfaceToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Reverse Direction"
		-iv reverseSurfaceEuc 0
		-iv reverseSurfaceLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q reverseSurfaceEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q reverseSurfaceLac` $goToTool;
	}
}

global proc reverseSurfaceValues ( string $toolName ) 
{
	performSurfaceReverse 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc reverseSurfaceProperties()
{
	scriptCtxCommonProperties;
}

proc createReverseSurfaceContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		reverseSurfaceToolSetup( 0, "" );
		scriptCtx -i1 "reverseSurface.png"
		  -title (uiRes("m_reverseSurfaceToolScript.kReverseSurfaceDirectionTool"))
		  -bcn "reverseSurface"

		  -exitUponCompletion `optionVar -q reverseSurfaceEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("reverseSurfaceToolScript 0")

		  -setNoSelectionPrompt ((uiRes("m_reverseSurfaceToolScript.kSelectSurfaceOrIsoparm")) )
		  -setSelectionPrompt ((uiRes("m_reverseSurfaceToolScript.kSelectMoreSurfacesorIsoparms")) )
		  -setDoneSelectionPrompt ((uiRes("m_reverseSurfaceToolScript.kPressEnterToComplete")) )

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q reverseSurfaceLac`
		  -setAllowExcessCount true
		  -showManipulators true

		  -setSelectionCount 0
		  -nurbsSurface true
		  -isoparm true
		$tool;
	}
}

global proc string reverseSurfaceToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolReverseSurface` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "reverseSurfaceContext";
			createReverseSurfaceContext $tool;
		}
	}
	return scriptToolScript( "performSurfaceReverse", $setToTool, $tool );
}
