// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
// Creation Date:  14 January 1999
//
//
//  Procedure Name:
//      planarToolScript
//
//  Description:
//	create wrapper around the planar command
//
//  Input Arguments:
//		0 ==> do the command.
//		1 ==> show the tool/action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool
//
//  Return Value:
//      None.
//

global proc planarToolSetup( int $forceFactorySettings, string $goToTool )
{
	optionVar -init $forceFactorySettings -category "Modeling.Surfaces.Planar"
		-iv planarTrimEuc 0
		-iv planarTrimLac 1
		;

	if( "" != $goToTool ) {
		scriptCtx -e -euc `optionVar -q planarTrimEuc` $goToTool;
		scriptCtx -e -lac `optionVar -q planarTrimLac` $goToTool;
	}
}

global proc planarValues ( string $toolName ) 
{
	performPlanarTrim 3 $toolName;
	scriptCtxCommonValues( $toolName );
}

global proc planarProperties()
{
	scriptCtxCommonProperties;
}

proc createPlanarContext( string $tool )
{
	if( ! `scriptCtx -exists $tool` ) {
		planarToolSetup( 0, "" );
		scriptCtx -i1 "planarTrim.png"
		  -title (uiRes("m_planarToolScript.kPlanarTool"))
		  -bcn "planar"

		  -exitUponCompletion `optionVar -q planarTrimEuc`
		  -totalSelectionSets 1
		  -expandSelectionList true
		  -fcs ("planarToolScript 0")

		  -setNoSelectionPrompt (uiRes("m_planarToolScript.kSelectPlanarNURBS"))
		  -setSelectionPrompt (uiRes("m_planarToolScript.kSelectAdditionalPlanarNURBS"))

		  -setAutoToggleSelection true
		  -setAutoComplete `optionVar -q planarTrimLac`
		  -showManipulators true

		  -setAllowExcessCount true
		  -setSelectionCount 0
		  -nurbsCurve true
		  -isoparm true
		  -curveOnSurface true
		  -surfaceEdge true
		  -polymeshEdge true
		$tool;
	}
}

global proc string planarToolScript( int $setToTool )
//
//	Description :
//		0 ==> do the command.
//		1 ==> show the action option box
//		2 ==> return the command
//		3 ==> show tool/action option box
//		4 ==> enter the tool/do the command
{
	string $tool = "";
	if( $setToTool > 2 ) {
		if( !`optionVar -q modelWithToolPlanar` ) {
			$setToTool = 4 - $setToTool;
		}
		else {
			$tool = "planarContext";
			createPlanarContext $tool;
		}
	}
	return scriptToolScript( "performPlanarTrim", $setToTool, $tool );
}
