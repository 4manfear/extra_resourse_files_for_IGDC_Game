// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:	June 18, 1997
//
//<doc>
//<name attributeExists>
//
//<synopsis>
//		attributeExists (string $attr, string $node)
//
//<returns>
//		int : 1 if the attribute exists on the node, else 0
//
//<description>
//      Check to see if the named attribute exists in the
//		given node.
//
//<flags>
//		string $attr Name of attribute to look for.
//		string $node Name of node that will be searched.
//
//<examples>
//  string $shapeName[] = `cone`;
//  if (`attributeExists "scaleX" $shapeName[0]`) {
//      print "Attribute exists\n";
//  } else {
//      print "Attribute does not exist\n";
//  }
//
//</doc>
/////////////////////////////////////////////////////////////////////////

global proc int attributeExists(string $attr, string $node)
{
	if ("" == $attr || "" == $node) return 0;

	// check zero, see if the node exists!
	//
	if( !`objExists $node` ) return 0;

	// First check to see if the attribute matches the short names
	//
	string $attrList[] = `listAttr -shortNames $node`;
	int $max = size($attrList);
	int $i;
	for( $i = 0; $i<$max; $i++ )
	{
		if( $attr == $attrList[$i] )
		{
			return 1;
		}
	}

	// Now check against the long names
	//
	$attrList = `listAttr $node`;
	$max = size($attrList);
	for( $i = 0; $i<$max; $i++ )
	{
		if( $attr == $attrList[$i] )
		{
			return 1;
		}
	}
	return 0;
}
