// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//
//
//
// Creation Date: July 21, 1998
//
//<doc>
//<name crossProduct>
//
//<synopsis>
//	float[] crossProduct( float $v1[], float $v2[], 
//  						int $normalizeInputs, int $normalizeResult )
//
//<description>
//		Given two float arrays of 3 values each, this procedure returns
//		the cross product of the two arrays as a float array of size 3.
//
//<flags>
//		float[]	$v1	First vector
//		float[]	$v2	Second vector
//		int	$normalizeInputs	1 means normalize $v1, $v2 before calculation
//		int	$normalizeResult	1 means normalize the return value
//
//<returns>
//		float[] : Cross product of the two vectors
//
//<examples>
//
//	float $v1[3] = {1,2,3};
//	float $v2[3] = {4,5,6};
//	crossProduct($v1, $v2, 0, 0);
//	// Results: -3 6 -3 //
//
//</doc>

global proc float[] crossProduct( float $v1[], float $v2[], 
								  int $normalizeInputs, int $normalizeResult )
{
	if ( size($v1) != 3 ) 
		warning (uiRes("m_crossProduct.kFirstArgument"));
	if ( size($v2) != 3 ) 
		warning (uiRes("m_crossProduct.kSecondArgument"));
	
	if ( $normalizeInputs == 1 )
	{
		// normalize the input vectors
		//
		normalize( $v1 );
		normalize( $v2 );
	}

	// the cross product
	//
	float $cross[3];
	$cross[0] = $v1[1]*$v2[2] - $v1[2]*$v2[1];
	$cross[1] = $v1[2]*$v2[0] - $v1[0]*$v2[2];
	$cross[2] = $v1[0]*$v2[1] - $v1[1]*$v2[0];

	if ( $normalizeResult == 1 )
	{
		// normalize the cross product result
		//
		normalize( $cross );
	}

	return $cross;
}
