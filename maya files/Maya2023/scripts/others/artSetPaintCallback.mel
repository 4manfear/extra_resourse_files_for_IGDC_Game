// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  May 2003
//
//  Description:
//      Option tool callbacks for Paint Set Membership tool.
//
//
//  Procedure Name:
//     	artSetPaintCallback 
//
//  Description:
//      Initialize the option values.
//
//  Input Arguments:
//
//  Return Value:
//      None.
//
// 

global proc artSetPaintOperationCallback(
	string 	$artCommand
)
{
	string $currContext = `currentCtx`;
	//
	//	Set Memebrship operation frameLayout
	//
	radioButtonGrp -e
		-on1 ($artCommand + " -e -setopertype \"add\" " + $currContext)
		-on2 ($artCommand + " -e -setopertype \"transfer\" " + $currContext)
		-on3 ($artCommand + " -e -setopertype \"remove\" " + $currContext)
		artSetPaintOperationRadio;
	
	// 
	// 	Set Membership flag frameLayout
	//
	textScrollList -e
		-sc ("artSetScrollListClick " + $artCommand ) 
	artSetPaintScrollList;

	textFieldGrp -e 
		-cc ($artCommand + " -e -settomodify \"#1\" " + $currContext)
		objectSetNameField;
}


global proc artSetScrollListClick(
	string $artCommand
)
{
	string $currContext = `currentCtx`;

	string $setNames[] = eval("textScrollList -q -si artSetPaintScrollList");
	string $name = $setNames[0];

	string $cmd = $artCommand + " -e -settomodify " + $name + " " + $currContext;
	eval( $cmd );

	$cmd = $artCommand + " -q -settomodify " + $currContext;
    textFieldGrp -e -text `eval $cmd` objectSetNameField;
}


// =======================================================
// Main Procedure.
// =======================================================

global proc artSetPaintCallback(
	string	$artCommand
)
{
	source "artisanCallback.mel";

	string $currContext = `currentCtx`;
	string $currTool	= `contextInfo -c $currContext`;

	// Basic Brush frameLayout.
	artisanBrushCallback( $artCommand, $currTool );

	// Set Paint Operation frameLayout.
	artSetPaintOperationCallback( $artCommand );

	// Stroke frameLayout.
	artisanStrokeCallback( $artCommand, $currTool );

	// Pressure frameLayout.
	artisanPressureCallback( $artCommand, $currTool );

	// Display frameLayout.
	artisanDisplayCallback( $artCommand, $currTool );
}
