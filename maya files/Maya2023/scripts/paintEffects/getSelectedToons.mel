// ===========================================================================
// Copyright 2022 Autodesk, Inc. All rights reserved.
//
// Use of this software is subject to the terms of the Autodesk license
// agreement provided at the time of installation or download, or which
// otherwise accompanies this software in either electronic or hard copy form.
// ===========================================================================
//
//
//  Creation Date:  2005
//
// Description: get selected toon nodes, or if there are none,
// toon related to the selected surface nodes
//	
global proc string[] getSelectedToons()
{
	
	string $toons[] = `ls -sl -dag -type "pfxToon"`;
	if( size( $toons ) == 0 ) {
		string $relatedToons[];
		string $objs[] = `ls -sl -dag -type "mesh"`;
		if( size( $objs ) > 0 ){
			$paintObj = $objs[0];
			string $cons[] = `listConnections -d 1 -type pfxToon ($paintObj + ".outMesh")`;
			for( $con in $cons  ){
				$relatedToons[size($relatedToons)] = $con;	
			}
		}	
		$objs = `ls -sl -dag -type "nurbsSurface"`;
		if( size( $objs ) > 0 ){
			$paintObj = $objs[0];
			string $cons[] = `listConnections -d 1 -type nurbsTessellate ($paintObj + ".local")`;
			if( size( $cons ) > 0 ){
				string $tess = $cons[0];	
				$cons = `listConnections -d 1 -type pfxToon ($tess + ".outputPolygon")`;
				for( $con in $cons  ){
					$relatedToons[size($relatedToons)] = $con;	
				}
			}
		}
		$objs = `ls -sl -dag -type "subdiv"`;
		if( size( $objs ) > 0 ){
			$paintObj = $objs[0];
			string $cons[] = `listConnections -d 1 -type subdivToPoly ($paintObj + ".outSubdiv")`;
			if( size( $cons ) > 0 ){
				string $tess = $cons[0];	
				$cons = `listConnections -d 1 -type pfxToon ($tess + ".outMesh")`;
				for( $con in $cons  ){
					$relatedToons[size($relatedToons)] = $con;	
				}
			}
		}
		// selected objects could have been sharing a toon outline
		$toons = stringArrayRemoveDuplicates($relatedToons);
	}
	return $toons;
}
